# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>

pkgname=libraw
pkgver=0.19.3
pkgrel=1
pkgdesc="A library for reading RAW files obtained from digital photo cameras (CRW/CR2, NEF, RAF, DNG, and others)"
arch=('x86_64')
url="https://www.libraw.org/"
license=(CDDL LGPL)
depends=(lcms2 jasper)
source=($pkgname-$pkgver.tar.gz::"https://github.com/LibRaw/LibRaw/archive/$pkgver.tar.gz")
sha256sums=('6c385d44d4d3b579770c7a2de1e887afb93521c75c3bf856095b4fa5e36ef3d3')

build() {
  cd LibRaw-$pkgver
  ./mkdist.sh
  ./configure --prefix=/usr \
    --disable-examples
  make
}

package() {
  cd LibRaw-$pkgver
  make DESTDIR="$pkgdir" install
}
