# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=kbd
pkgver=2.2.0
pkgrel=2
pkgdesc="Keytable files and keyboard utilities"
arch=('x86_64')
url="http://www.kbd-project.org"
license=('GPL')
depends=('glibc' 'pam')
makedepends=('check' 'git')
#source=(https://www.kernel.org/pub/linux/utils/${pkgname}/${pkgname}-${pkgver}.tar.gz
#        https://www.kernel.org/pub/linux/utils/kbd/${pkgname}-${pkgver}.tar.sign
source=(git+https://git.kernel.org/pub/scm/linux/kernel/git/legion/kbd.git#tag=v$pkgver
        'fix-euro2.patch'
        kbd-dont-override-cflags.patch::"https://github.com/legionus/kbd/commit/15a74479.patch"
        kbd-31.patch::"https://github.com/legionus/kbd/commit/acf93e44.patch"
        kbd-32.patch::"https://github.com/legionus/kbd/commit/7e27102b.patch")
provides=('vlock')
conflicts=('vlock')
replaces=('vlock')
md5sums=('SKIP'
         'd869200acbc0aab6a9cafa43cb140d4e'
         'f33bf61aa3e57befa0408c7578c81c7d'
         '9e515a98f7ee1a2c1562c0e93467fa6d'
         '9fc8d774887a55a7e4bc79240a2986c5')
validpgpkeys=(
              '7F2A3D07298149A0793C9A4EA45ABA544CFFD434' #Alexey Gladkov 
             )

prepare() {
  cd ${srcdir}/${pkgname}
  # rename keymap files with the same names
  # this is needed because when only name of keymap is specified
  # loadkeys loads the first keymap it can find, which is bad (see FS#13837)
  # this should be removed when upstream adopts the change
  mv data/keymaps/i386/qwertz/cz{,-qwertz}.map
  mv data/keymaps/i386/olpc/es{,-olpc}.map
  mv data/keymaps/i386/olpc/pt{,-olpc}.map
  mv data/keymaps/i386/fgGIod/trf{,-fgGIod}.map
  mv data/keymaps/i386/colemak/{en-latin9,colemak}.map
  # fix euro2 #28213
  patch -Np1 -i ../fix-euro2.patch
  git cherry-pick -n 15a74479 # Don't override CFLAGS
  git cherry-pick -n acf93e44 # https://github.com/legionus/kbd/issues/31
  git cherry-pick -n 7e27102b # https://github.com/legionus/kbd/issues/32
  autoreconf -if
}

build() {
  cd ${pkgname}
  ./configure --prefix=/usr --datadir=/usr/share/kbd --mandir=/usr/share/man
  make KEYCODES_PROGS=yes RESIZECONS_PROGS=yes
}

package() {
  cd ${pkgname}
  make KEYCODES_PROGS=yes RESIZECONS_PROGS=yes DESTDIR=${pkgdir} install
}
