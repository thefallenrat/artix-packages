# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: eric <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=tdb
pkgver=1.3.18
pkgrel=2
pkgdesc="A Trivial Database similar to GDBM but allows simultaneous commits"
arch=(x86_64)
license=('GPL3')
url="http://tdb.samba.org/"
makedepends=('python' 'libxslt' 'docbook-xsl')
optdepends=('python: for python bindings')
source=(http://samba.org/ftp/${pkgname}/${pkgname}-${pkgver}.tar.gz
        http://samba.org/ftp/${pkgname}/${pkgname}-${pkgver}.tar.asc)
validpgpkeys=('9147A339719518EE9011BCB54793916113084025') #Samba Library Distribution Key <samba-bugs@samba.org> 
build() {
   cd ${srcdir}/${pkgname}-${pkgver}
   ./configure --prefix=/usr \
               --localstatedir=/var \
               --sysconfdir=/etc/samba
   make
}

package() {
   cd ${srcdir}/${pkgname}-${pkgver}
   make DESTDIR=$pkgdir/ install
}
md5sums=('fdb34ed48478a084e9e0c310cc178e87'
         'SKIP')
