# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: eric <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=tdb
pkgver=1.4.3
pkgrel=1
pkgdesc="A Trivial Database similar to GDBM but allows simultaneous commits"
arch=(x86_64)
license=('GPL3')
url="https://tdb.samba.org/"
makedepends=('python' 'libxslt' 'docbook-xsl')
optdepends=('python: for python bindings')
source=(https://samba.org/ftp/${pkgname}/${pkgname}-${pkgver}.tar.gz
        https://samba.org/ftp/${pkgname}/${pkgname}-${pkgver}.tar.asc
        samba-update-waf-for-py38.patch::https://gitlab.com/samba-team/samba/commit/aabdcc9151.patch
        wafsamba-use-native-waf-timer.patch)
validpgpkeys=('9147A339719518EE9011BCB54793916113084025') #Samba Library Distribution Key <samba-bugs@samba.org>
sha512sums=('99488839e7da396f04df60412d21a7d3e09efeab52772d6cb5e9470a3dfd585d73ef2422c51cd0d8ccc123a65d455de400d5d6b24a21a2a50d3da60d9a70e67a'
            'SKIP'
            'fc15527eb901620dfa90c7bd91ebffc57c143f67db18377ac5129f8edc3db61b0e4149d3d793559bc5f9ebbe4f040231d12db45bd5cfb3a46df2e73f02f370b2'
            '22ebd9a15833ed2a4eaa8f9a71dc71c55465f2c7a1d5c0afcd211a202b7c11dd7d43fc4848863cbcc69206f0815afc411367c007032dd1aaa3edba81238729b0')

build() {
   cd ${pkgname}-${pkgver}
   ./configure --prefix=/usr \
               --localstatedir=/var \
               --sysconfdir=/etc/samba
   make
}

package() {
   cd ${pkgname}-${pkgver}
   make DESTDIR="$pkgdir/" install
}
