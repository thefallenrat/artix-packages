# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jeremy Cowgar <jeremy@cowgar.com>

pkgbase=check
pkgname=(check check-docs)
pkgver=0.13.0
pkgrel=1
pkgdesc="A unit testing framework for C"
url="https://libcheck.github.io/check/"
arch=(x86_64)
license=(LGPL)
depends=(awk)
makedepends=(git texlive-bin texi2html doxygen)
_commit=90d03f3fe002c33224432753ebfa21ebb5c32238  # tags/0.13.0
source=("git+https://github.com/libcheck/check#commit=$_commit")
md5sums=('SKIP')

pkgver() {
  cd check
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd check
  autoreconf -fvi
}

build() {
  cd check
  ./configure --prefix=/usr --disable-static
  make all doc/doxygen doc/check_html
}

check() {
  cd check
  # Extremely long
  #make -k check
}

package_check() {
  optdepends+=('check-docs: Documentation')

  cd check
  make DESTDIR="$pkgdir" install

  mv "$pkgdir/usr/share/doc" "$srcdir"
}

package_check-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  cp -a check/doc/doxygen/html doc/check/doxygen
  cp -a check/doc/check_html   doc/check/manual

  mkdir -p "$pkgdir/usr/share"
  mv doc "$pkgdir/usr/share"
}
