# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ignacio Galmarino <igalmarino@gmail.com>

pkgname=openresolv
pkgver=3.9.2
pkgrel=1
pkgdesc="resolv.conf management framework (resolvconf)"
arch=(any)
url="https://roy.marples.name/projects/openresolv"
license=('custom:BSD')
backup=(etc/resolvconf.conf)
provides=(resolvconf)
depends=(bash)
makedepends=(eudev)
source=("https://roy.marples.name/downloads/openresolv/${pkgname}-${pkgver}.tar.xz")
sha256sums=('14d7e241682e5566f6b8bf4c7316c86d7a9b8e7ed48e1de4967dbf1ea84ed373')

prepare() {
  cd $pkgname-$pkgver
  sed -n '2,25{s:^# \?::;p}' resolvconf.in >LICENSE
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/resolvconf --sbindir=/usr/bin
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
