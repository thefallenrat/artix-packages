# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=openmp
pkgver=8.0.0
pkgrel=1
pkgdesc="LLVM OpenMP Runtime Library"
arch=('x86_64')
url="https://openmp.llvm.org/"
license=('custom:University of Illinois/NCSA Open Source License')
depends=('glibc' 'libelf' 'libffi')
makedepends=('cmake' 'ninja')
source=(https://releases.llvm.org/$pkgver/$pkgname-$pkgver.src.tar.xz{,.sig})
sha256sums=('f7b1705d2f16c4fc23d6531f67d2dd6fb78a077dd346b02fed64f4b8df65c9d5'
            'SKIP')
validpgpkeys+=('B6C8F98282B944E3B0D5C2530FC3042E345AD05D') # Hans Wennborg <hans@chromium.org>
validpgpkeys+=('474E22316ABF4785A88C6E8EA2C794A986419D8A') # Tom Stellard <tstellar@redhat.com>

prepare() {
	cd "$pkgname-$pkgver.src"
	mkdir build
}

build() {
	cd "$pkgname-$pkgver.src/build"

  cmake .. -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd "$pkgname-$pkgver.src/build"

  DESTDIR="$pkgdir" ninja install
  install -Dm644 ../LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # libgomp.so is also provided by gcc-libs; remove it to fix the conflict
  rm "$pkgdir/usr/lib/libgomp.so"

}

# vim:set ts=2 sw=2 et:
