# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=xapian-core
epoch=1
pkgver=1.4.12
pkgrel=1
pkgdesc='Open source search engine library.'
arch=('x86_64')
url='https://www.xapian.org/'
license=('GPL')
depends=('util-linux')
# xapian config requires libxapian.la
options=('libtool')
source=("https://oligarchy.co.uk/xapian/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('230b39e012366267cf5ea09d01d3b463818c0d05861b8c8643bc6fc238be21bd3f4862adeba864b4af2962b1c0748b3d380dfe845da467f5164b0d938288c430'
            'SKIP')
validpgpkeys=('08E2400FF7FE8FEDE3ACB52818147B073BAD2B07') # Olly Betts <olly@debian.org>

build() {
  # FS#40614
  if [ "${CARCH}" = "i686" ]; then
    SSE2="--disable-sse"
  fi

  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr ${SSE2}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
