# Maintainer: Arch Linux Team
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

pkgname=base
pkgver=2
pkgrel=2.1
pkgdesc='Minimal package set to define a basic Artix Linux installation'
url='https://www.artixlinux.org'
arch=('any')
license=('GPL')
depends=(
  # very very base
  'filesystem' 'gcc-libs' 'glibc' 'bash'

  # POSIX tools
  'coreutils' 'file' 'findutils' 'gawk' 'grep' 'procps-ng' 'sed' 'tar'

  # standard linux toolset
  'gettext' 'pciutils' 'psmisc' 'shadow' 'util-linux' 'bzip2' 'gzip' 'xz'

  # distro defined requirements
  'licenses' 'pacman' 'init-elogind'

  # networking, ping, etc
  'iputils' 'iproute2'
)
optdepends=('linux: bare metal support')
provides=('artix-sysvcompat')
conflicts=('artix-sysvcompat')
replaces=('artix-sysvcompat')
source=('initswitch-hook'
        '55-initswitch.hook'
        '50-sysvcompat.hook')
sha256sums=('dab2a5f959b37d4f489111d999fec31f8c03d07ca9ec26400b0e556466b50b29'
            '97cac34e626d54de123662387360e60be4c4a654581bce1be81ef1cc2cc8eea0'
            '2a5fa67f95da713b67323c3d6cfd4e5cc6339847acd30edfea154d66cadb3b76')

package() {
	# pacman hooks
    install -Dm755 ${srcdir}/initswitch-hook "${pkgdir}"/usr/share/libalpm/scripts/initswitch-hook
    install -Dm644 -t "${pkgdir}"/usr/share/libalpm/hooks ${srcdir}/*.hook

    # provide default symlink for PM file tracking
    install -d "${pkgdir}"/usr/bin
    ln -sf openrc-init "${pkgdir}"/usr/bin/init
}
