# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Marco A Rojas <marquicus at gmail dot com>

pkgname=ldb
pkgver=1.5.5
pkgrel=2
epoch=1
pkgdesc="A schema-less, ldap like, API and database"
arch=('x86_64')
url="https://ldb.samba.org/"
source=(https://samba.org/ftp/${pkgname}/${pkgname}-${pkgver}.tar.gz
        https://samba.org/ftp/${pkgname}/${pkgname}-${pkgver}.tar.asc
        samba-update-waf-for-py38.patch::https://gitlab.com/samba-team/samba/commit/aabdcc9151.patch)
license=('GPL3')
depends=('talloc' 'tevent' 'tdb' 'popt' 'lmdb')
makedepends=('python' 'cmocka' 'docbook-xsl')
optdepends=('python: for python bindings')
validpgpkeys=('9147A339719518EE9011BCB54793916113084025') # Samba Library Distribution Key <samba-bugs@samba.org> 

prepare() {
	cd ${pkgname}-${pkgver}

	# https://bugzilla.samba.org/show_bug.cgi?id=13960
	patch -Np1 -i ../samba-update-waf-for-py38.patch
}

build() {
	cd ${pkgname}-${pkgver}

	./configure --prefix=/usr \
		--disable-rpath \
		--disable-rpath-install \
		--bundled-libraries=NONE \
		--builtin-libraries=replace \
                --with-modulesdir=/usr/lib/ldb/modules \
                --with-privatelibdir=/usr/lib/ldb
	make
}

package() {
	cd ${pkgname}-${pkgver}

	make DESTDIR="${pkgdir}/" install
}
md5sums=('339a41ac9efaec680e6d1469dd9aa35b'
         'SKIP'
         '74e627915f28a848e046d37055d86f3b')
