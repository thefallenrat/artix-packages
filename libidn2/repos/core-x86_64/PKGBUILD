# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Daurnimator <quae@daurnimator.com>

pkgname=libidn2
pkgver=2.1.1
pkgrel=2
pkgdesc="Free software implementation of IDNA2008, Punycode and TR46"
arch=('x86_64')
url="https://www.gnu.org/software/libidn/#libidn2"
license=('GPL2' 'LGPL3')
depends=('libunistring')
makedepends=('ruby-ronn')
provides=('libidn2.so')
source=(https://ftp.gnu.org/gnu/libidn/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('95416080329298a13269e13175041b530cec3d98b54cafae9424b8dfd22078b1'
            'SKIP')
validpgpkeys=('1CB27DBC98614B2D5841646D08302DB6A2670428')

prepare() {
  cd $pkgname-$pkgver
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}
package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
