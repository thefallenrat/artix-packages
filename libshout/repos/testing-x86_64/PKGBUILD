# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: arjan <arjan@archlinux.org>
# Contributor: Jason Chu <jchu@xentac.net>

pkgname=libshout
pkgver=2.4.2
pkgrel=1
epoch=1
pkgdesc="Library for accessing a shoutcast/icecast server"
arch=('x86_64')
depends=('libvorbis' 'libtheora' 'speex' 'openssl')
url="https://www.icecast.org/"
options=('!emptydirs')
license=('LGPL')
source=("https://downloads.xiph.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('ae4b042009887de22d105eb8238f4362f6ff51bd645fd51b5a87c4b5e3f5fdc99e9f99f85c24ae2e9d23eab92b854a7e733bf9f8cc61a8362ce984f02a014e93')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -e 's/SSLeay_add_all_algorithms/OpenSSL_add_all_algorithms/g' -i src/tls.c
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
