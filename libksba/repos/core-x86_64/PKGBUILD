# Maintainer: 
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=libksba
pkgver=1.3.5
pkgrel=2
pkgdesc="A CMS and X.509 access library"
arch=(x86_64)
license=('GPL')
url="https://www.gnupg.org/related_software/libksba/index.html"
depends=('bash' 'libgpg-error' 'glibc')
source=(https://www.gnupg.org/ftp/gcrypt/$pkgname/$pkgname-$pkgver.tar.bz2{,.sig})
sha1sums=('a98385734a0c3f5b713198e8d6e6e4aeb0b76fde'
          'SKIP')
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6') # Werner Koch

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
   cd $pkgname-$pkgver
   make check
}  

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
