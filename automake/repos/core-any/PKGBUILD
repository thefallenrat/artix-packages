# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=automake
pkgver=1.16.1
pkgrel=2
pkgdesc="A GNU tool for automatically creating Makefiles"
arch=('any')
license=('GPL')
url="https://www.gnu.org/software/automake"
groups=('base-devel')
depends=('perl' 'bash')
makedepends=('autoconf')
checkdepends=('dejagnu' 'gcc-fortran' 'java-environment' 'vala' 'emacs' 'cscope'
              'python2' 'python-virtualenv')
source=(https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig}
        automake-1.15-dejagnu-testcase.patch)
sha256sums=('5d05bb38a23fd3312b10aea93840feec685bdf4a41146e78882848165d3ae921'
            'SKIP'
            '787b0cc0f94cc28f86169a5482189ec8ed784c99312939534788d2f5841eae77')
validpgpkeys=('E1622F96D2BB4E58018EEF9860F906016E407573'   # Stefano Lattarini
              'F2A38D7EEB2B66405761070D0ADEE10094604D37')  # Mathieu Lirzin

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # fix testsuite issue with resent dejagnu
  patch -p1 -i $srcdir/automake-1.15-dejagnu-testcase.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --build=$CHOST --prefix=/usr
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check PYTHON=/usr/bin/python2
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
