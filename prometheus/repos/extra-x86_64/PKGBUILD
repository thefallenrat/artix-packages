# Maintainer : Rafli Akmal <rafliakmaltejakusuma@gmail.com>
# Contributor: Oscar Campos <damnwidget@artixlinux.org>

pkgname=prometheus
pkgver=2.0.0
pkgrel=1
pkgdesc="Monitoring system and time series database."
url="http://prometheus.io"
arch=('i686' 'x86_64')
license=('APACHE')
makedepends=('go' 'git')
backup=("etc/prometheus/prometheus.yml")
source=("https://github.com/prometheus/$pkgname/archive/v$pkgver.tar.gz"
	"${pkgname}.sysusers"
  "${pkgname}.tmpfiles")
md5sums=('60be2bf2cce88a09a11dfe8c2bcd435d'
         '9afdaba8dc6fb0632bfd797563d5fcdd'
         'e4961723077b6da0fd6ff1e480635789')

prepare() {
  # create gopath and link sources
  msg2 'GOPATH setup'
  export GOPATH="$srcdir/gopath"
  mkdir -p "$GOPATH/src/github.com/prometheus/"
  ln -fsrT "$srcdir/$pkgname-$pkgver/" "$GOPATH/src/github.com/prometheus/$pkgname"

  cd "$GOPATH/src/github.com/prometheus/$pkgname"
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      msg2 "Applying patch ${filename##*/}"
      patch -Np 1 -i "$srcdir/${filename##*/}"
    fi
  done
  :

  msg2 "Rebuilding prometheus web bindata..."
  go get -u github.com/jteeuwen/go-bindata/...
  cd "$srcdir/$pkgname-$pkgver"
  ../gopath/bin/go-bindata -pkg ui -o web/ui/bindata.go -ignore '(.*\.map|bootstrap\.js|bootstrap-theme\.css|bootstrap\.css)'  web/ui/templates/... web/ui/static/...
  go fmt ./web/ui
}

build() {
  export GOPATH="$srcdir/gopath"
  cd "$GOPATH/src/github.com/prometheus/$pkgname"
  make build
}

# check() {
  # export GOPATH="$srcdir/gopath"
  # cd "$GOPATH/src/github.com/prometheus/$pkgname"
  # make test
# }

package() {
  install -Dm644 "$pkgname.sysusers" "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -Dm644 "$pkgname.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

  cd $pkgname-$pkgver
  install -Dsm755 prometheus "$pkgdir/usr/bin/$pkgname"
  install -Dsm755 promtool "$pkgdir/usr/bin/promtool"
  install -dm755 "$pkgdir/etc/$pkgname"
  install -m644 "documentation/examples/prometheus.yml" "$pkgdir/etc/prometheus"
  install -dm755 "$pkgdir/usr/share/prometheus"

  cp -r consoles "$pkgdir/usr/share/prometheus"
  cp -r console_libraries "$pkgdir/usr/share/prometheus"
  ln -s /usr/share/prometheus/consoles "$pkgdir/etc/prometheus/consoles"
  ln -s /usr/share/prometheus/console_libraries "$pkgdir/etc/prometheus/console_libraries"

  install -dm755 "$pkgdir/usr/share/prometheus/web"
  cp -r web/* "$pkgdir/usr/share/prometheus/web"
}

# vim:set ts=2 sw=2 et:
