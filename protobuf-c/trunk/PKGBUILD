# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Aurélien Wailly <aurelien.wailly@gmail.com>

pkgname=protobuf-c
pkgver=1.3.1
pkgrel=2
pkgdesc='Protocol Buffers implementation in C'
arch=('x86_64')
url='https://github.com/protobuf-c/protobuf-c'
license=('BSD')
depends=('protobuf')
provides=('libprotobuf-c.so')
source=("$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz"
        invalid-namespace.patch)
sha256sums=('51472d3a191d6d7b425e32b612e477c06f73fe23e07f6a6a839b11808e9d2267'
            '050306bae86af55f90606613d3c362c3c93af779aa6be3e639c6a1df3c228c87')

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i ../invalid-namespace.patch
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
