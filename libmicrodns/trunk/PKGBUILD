# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Niklas <dev@n1klas.net>

pkgname=libmicrodns
pkgver=0.1.0
pkgrel=1
pkgdesc='Minimal mDNS resolver library'
url='https://github.com/videolabs/libmicrodns'
arch=('x86_64')
license=('LGPL2.1')
depends=('glibc')
provides=('libmicrodns.so')
source=(https://github.com/videolabs/libmicrodns/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('aa78896719c60553d0310c236937dad0f5cb05ef0fdd33a0503aa9af984083b430b3b517ae174b116352d02d940282b17c5fb3fc4a67fd55dd8895a5773fd161')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
