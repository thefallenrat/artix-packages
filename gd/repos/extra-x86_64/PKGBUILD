# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=gd
pkgver=2.2.5
pkgrel=2
pkgdesc="Library for the dynamic creation of images by programmers"
arch=('x86_64')
url="https://libgd.github.io/"
license=('custom')
depends=('fontconfig' 'libxpm' 'libwebp')
optdepends=('perl: bdftogd script')
checkdepends=('ttf-liberation')
source=("https://github.com/libgd/libgd/releases/download/gd-${pkgver}/libgd-${pkgver}.tar.xz"
        'gd-CVE-2018-1000222.patch' 'gd-CVE-2018-5711.patch'
        'gd-CVE-2019-6977.patch' 'gd-CVE-2019-6978.patch')
sha1sums=('b777b005c401b6fa310ccf09eeb29f6c6e17ab2c'
          'f17097d44735face67cf5eb3c85878f45d0f72c9'
          '4a4c0acc19ee4d5ceb6dd8b090b65381a39bf18e'
          'bd2444b04cf648d9bc60268f0c890c5950a46d36'
          'fc04932562f7a806a6041605bde43976c173a646')

prepare() {
  cd libgd-${pkgver}
  # security patches from openSUSE:
  # https://build.opensuse.org/package/show/openSUSE:Factory/gd
  patch -p1 -i "${srcdir}/gd-CVE-2018-1000222.patch"
  patch -p1 -i "${srcdir}/gd-CVE-2018-5711.patch"
  patch -p1 -i "${srcdir}/gd-CVE-2019-6977.patch"
  patch -p1 -i "${srcdir}/gd-CVE-2019-6978.patch"
}

build() {
  cd libgd-${pkgver}
  ./configure \
    --prefix=/usr \
    --disable-rpath
  make
}

check() {
  cd libgd-${pkgver}
  make check
}

package() {
  cd libgd-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
