# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mihai Bi≈üog <mihai.bisog@gmail.com>

pkgname=fmt
pkgver=6.1.1
pkgrel=1
pkgdesc='Open-source formatting library for C++'
arch=(x86_64)
url=https://fmtlib.net
license=(BSD)
makedepends=(
  cmake
  git
  ninja
)
provides=(libfmt.so)
source=(git+https://github.com/fmtlib/fmt.git#tag=071794ec654a7c503b6214de4032c8ec8a07440b)
sha256sums=(SKIP)

pkgver() {
  cd fmt

  git describe --tags
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  cmake ../fmt \
    -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DBUILD_SHARED_LIBS=1
  cmake --build .
}

check() {
  cmake --build build --target test
}

package() {
  DESTDIR="${pkgdir}" cmake --build build --target install
  install -Dm 644 fmt/LICENSE.rst -t "${pkgdir}"/usr/share/licenses/fmt
}

# vim: ts=2 sw=2 et:
