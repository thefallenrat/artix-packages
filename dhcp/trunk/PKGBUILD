# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Isenmann <daniel @archlinux.org>

pkgbase=dhcp
pkgname=('dhcp' 'dhclient')

# separate patch levels with a period to maintain proper versioning.
pkgver=4.4.2
pkgrel=2
arch=('x86_64')
license=('custom:isc-dhcp')
url='https://www.isc.org/dhcp/'
makedepends=('bash' 'iproute2' 'openldap')
validpgpkeys=('AE3FAC796711EC59FC007AA474BB6B9A4CBB3D38') # Internet Systems Consortium, Inc. (Signing key, 2019-2020) <codesign@isc.org>
source=("https://downloads.isc.org/isc/${pkgbase}/${pkgver}/${pkgbase}-${pkgver}.tar.gz"{,.asc}
        'dhcp-sysusers.conf'
        'dhcp-tmpfiles.conf'
        '0002-iproute2.patch')
sha256sums=('1a7ccd64a16e5e68f7b5e0f527fd07240a2892ea53fe245620f4f5f607004521'
            'SKIP'
            'b16083e6bb572ffacaa7cd97e7fde5fcfa1b6dbeb166f162e2ec6e8ec4b928d6'
            'abcd30e9e8428e34d22ab4d3074ef4bd84c2b11f5868597111b47d6f56d204da'
            '837a64189b949afae951655546967cc8f17f2f2cf370faabff00575364f0fcf7')

prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  # no ifconfig, use ip from iproute2
  patch -Np1 -i ../0002-iproute2.patch
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  ./configure \
      --prefix=/usr \
      --sbindir=/usr/bin \
      --sysconfdir=/etc \
      --enable-binary-leases \
      --enable-paranoia \
      --with-ldap \
      --with-ldapcrypto \
      --with-srv-lease-file=/var/lib/dhcp/dhcpd.leases \
      --with-srv6-lease-file=/var/lib/dhcp/dhcpd6.leases \
      --with-cli-lease-file=/var/lib/dhclient/dhclient.leases \
      --with-cli6-lease-file=/var/lib/dhclient/dhclient6.leases

  make -j1
}

package_dhcp(){
  pkgdesc="A DHCP server, client, and relay agent"
  depends=('glibc' 'libldap')
  backup=('etc/dhcpd.conf' 'etc/dhcpd6.conf')

  cd "${srcdir}/${pkgbase}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/dhcp-sysusers.conf" "${pkgdir}/usr/lib/sysusers.d/dhcp.conf"
  install -D -m644 "${srcdir}/dhcp-tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/dhcp.conf"

  # move and install config files in place
  mv "${pkgdir}/etc/dhcpd.conf.example" "${pkgdir}/etc/dhcpd.conf"
  install -D -m644 doc/examples/dhcpd-dhcpv6.conf "${pkgdir}/etc/dhcpd6.conf"

  # Remove dhclient
  make -C client DESTDIR="${pkgdir}" uninstall

  # install license
  install -m644 -D LICENSE "${pkgdir}/usr/share/licenses/dhcp/LICENSE"
}

package_dhclient(){
  pkgdesc="A standalone DHCP client from the dhcp package"
  depends=('glibc' 'bash' 'iproute2' 'run-parts')
  provides=('dhcp-client')

  cd "${srcdir}/${pkgbase}-${pkgver}"

  make -C client DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/dhclient"
  mv "${pkgdir}/etc/dhclient.conf.example" "${pkgdir}/usr/share/dhclient/"

  install -d "${pkgdir}/var/lib/dhclient"

  # install dhclient linux script
  install -m755 client/scripts/linux "${pkgdir}/usr/bin/dhclient-script"

  # install license
  install -m644 -D LICENSE "${pkgdir}/usr/share/licenses/dhclient/LICENSE"
}
