# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gtk-doc
pkgver=1.30+1+g44c5972
pkgrel=2
pkgdesc="Documentation tool for public library API"
url="https://www.gtk.org/gtk-doc/"
arch=(any)
license=(GPL FDL)
depends=(docbook-xsl docbook-xml glib2-docs python-lxml python-pygments
         python-anytree)
makedepends=(dblatex git yelp-tools meson)
checkdepends=(bc gtk3 python-six python-parameterized)
optdepends=('dblatex: PDF support')
_commit=44c5972b167176f15cc32fc46743b596b7f00fe5  # master~2
source=("git+https://gitlab.gnome.org/GNOME/gtk-doc.git#commit=$_commit"
        fixperms.diff)
sha256sums=('SKIP'
            'bd8489e54c2e089197a692482c6f25b237b99db2b0419c4dcc3f29ce6f21e7e4')

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/GTK_DOC_//' -e 's/_/\./g' -e 's/-/+/g'
}

prepare() {
  cd $pkgname
  # https://gitlab.gnome.org/GNOME/gtk-doc/issues/84
  patch -Np1 -i ../fixperms.diff
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  # Tests don't work yet
  meson test -C build --print-errorlogs || :
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
