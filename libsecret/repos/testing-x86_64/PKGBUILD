# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libsecret
pkgver=0.20.0
pkgrel=1
pkgdesc="Library for storing and retrieving passwords and other secrets"
url="https://wiki.gnome.org/Projects/Libsecret"
arch=(x86_64)
license=(LGPL)
depends=(glib2 libgcrypt)
checkdepends=(python-dbus dbus-glib python-gobject gjs)
makedepends=(docbook-xsl gobject-introspection vala git gtk-doc meson valgrind)
optdepends=('org.freedesktop.secrets: secret storage backend')
_commit=3939b03f39dfef9fa9c54f709d094893b90d82a0  # tags/0.20.0^0
source=("git+https://gitlab.gnome.org/GNOME/libsecret.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # Build fixes
  git cherry-pick -n \
    cf21ad50b62f7c8e4b22ef374f0a73290a99bdb8 \
    e6881d4ff1260c7308be56f21a9d354d7680dfaa \
    2e85b13f8f3512f9cef0bed7cf8b6734af06bc9b
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  dbus-run-session meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
