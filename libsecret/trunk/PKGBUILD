# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libsecret
pkgver=0.19.0
pkgrel=2
pkgdesc="Library for storing and retrieving passwords and other secrets"
url="https://wiki.gnome.org/Projects/Libsecret"
arch=(x86_64)
license=(LGPL)
depends=(glib2 libgcrypt)
checkdepends=(python-dbus dbus-glib python-gobject gjs)
makedepends=(docbook-xsl gobject-introspection vala git gtk-doc meson valgrind)
optdepends=('gnome-keyring: key storage service, or use any other service implementing org.freedesktop.secrets')
_commit=be541cf9d4a81b97f55794a5b1de50f92aac1fbb  # tags/0.19.0^0
source=("git+https://gitlab.gnome.org/GNOME/libsecret.git#commit=$_commit"
        0001-service-Fix-secret_service_ensure_session_finish-err.patch)
sha256sums=('SKIP'
            '94de310cf02d67ad8447bebf8f55bf2e1c6d864e13fb16442b2eb09ee0b93acb')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # https://bugs.archlinux.org/task/63666
  git apply -3 ../0001-service-Fix-secret_service_ensure_session_finish-err.patch
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  dbus-run-session meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
