# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=whois
pkgver=5.4.2
pkgrel=1
pkgdesc='Intelligent WHOIS client'
url='https://github.com/rfc1036/whois'
license=('GPL')
arch=('x86_64')
depends=('libidn2')
makedepends=('perl')
source=("http://ftp.debian.org/debian/pool/main/w/whois/${pkgname}_${pkgver}.tar.xz")
sha256sums=('eee33a3b3a56912fbf115a7dd24ed60314e2707a3ad6aa604ca2752c1ed01f57')

backup=('etc/whois.conf')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make \
		CONFIG_FILE=/etc/whois.conf \
		CFLAGS="$CFLAGS $CPPFLAGS" \
		HAVE_ICONV=1
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make prefix=/usr BASEDIR="${pkgdir}" install-whois
	install -D -m644 whois.conf "${pkgdir}/etc/whois.conf"
}
