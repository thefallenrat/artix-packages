# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Roman Cheplyaka <roma@ro-che.info>

pkgname=libedit
_pkgver=20191025-3.1
pkgver=${_pkgver/-/_}
pkgrel=1
pkgdesc='Command line editor library providing generic line editing, history, and tokenization functions'
url='https://thrysoee.dk/editline/'
arch=('x86_64')
license=('BSD')
depends=('ncurses')
source=("https://thrysoee.dk/editline/libedit-${_pkgver}.tar.gz")
sha256sums=('6dff036660d478bfaa14e407fc5de26d22da1087118c897b1a3ad2e90cb7bf39')

build() {
	cd "${srcdir}/${pkgname}-${_pkgver}"
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${_pkgver}"
	make prefix="${pkgdir}"/usr install

	rm "${pkgdir}"/usr/share/man/man3/history.3 # conflicts with readline
	cp "${pkgdir}"/usr/share/man/man3/editline.3 "${pkgdir}"/usr/share/man/man3/el.3
	install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/libedit/LICENSE
}
