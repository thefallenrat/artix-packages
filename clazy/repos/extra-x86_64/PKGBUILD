# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andr√© Klitzing <aklitzing () gmail () com>

pkgname=clazy
pkgver=1.5
pkgrel=3
pkgdesc="Qt oriented code checker based on clang framework"
url="https://www.kdab.com/"
license=(GPL2)
depends=(clang)
makedepends=(cmake llvm)
arch=(x86_64)
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('87e37aace395e31359d4258452bbf7d1bdb5eae39e21e0dc0d80a75db8b8b779'
            'SKIP')
validpgpkeys=(949014B23D24354DFD548E5457416A0ADCEF0EFE) # Sergio Martins <sergio.martins@kdab.com>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
