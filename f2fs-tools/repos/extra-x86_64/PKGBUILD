# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Christian Hesse <mail@eworm.de>

pkgname=f2fs-tools
pkgver=1.13.0
pkgrel=1
pkgdesc='Tools for Flash-Friendly File System (F2FS)'
arch=('x86_64')
url='https://git.kernel.org/pub/scm/linux/kernel/git/jaegeuk/f2fs-tools.git/about/'
depends=('util-linux')
makedepends=('git')
license=('GPL')
validpgpkeys=('D3452A79D8C2B4EAC656F4224014A87E824850D2') # Jaegeuk Kim <jaegeuk@kernel.org>
source=("git+https://git.kernel.org/pub/scm/linux/kernel/git/jaegeuk/f2fs-tools.git#tag=v${pkgver}?signed")
sha256sums=('SKIP')

prepare() {
	cd "${pkgname}"

	# /usr/bin/sg_write_buffer is provided in sg3_utils
	sed -i '/sg_write_buffer/d' tools/Makefile.am

	autoreconf -fi
}

build() {
	cd "${pkgname}"

	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin
	make
}

package() {
	cd "${pkgname}"

	make DESTDIR="${pkgdir}/" sbindir=/usr/bin install
}
