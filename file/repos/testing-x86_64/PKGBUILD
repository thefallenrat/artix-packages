# Maintainer: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=file
pkgver=5.37
pkgrel=5
pkgdesc='File type identification utility'
arch=('x86_64')
license=('custom')
groups=('base-devel')
url='https://www.darwinsys.com/file/'
depends=('glibc' 'zlib')
source=("ftp://ftp.astron.com/pub/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc}
  d3132f65594e8ad27b74643786a786cd3e60c715.patch)
validpgpkeys=('BE04995BA8F90ED0C0C176C471112AB16CB33B3A') # Christos Zoulas
sha256sums=('e9c13967f7dd339a3c241b7710ba093560b9a33013491318e88e6b8b57bae07f'
            'SKIP'
            '2ef553d3d4c68fe3e3850bc6151e66ce5714d774536deb9b8401425e73214ea7')

prepare() {
  cd $pkgname-$pkgver
  # apply patch from the source array (should be a pacman feature)
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $pkgname-$pkgver

  # Fix linking libmagic (vfork needs libpthread)
  CFLAGS+=" -pthread"

  # seccomp breaks file -z foo.xz
  # does a whole pipe-fork-exec dance with /bin/xz, so difficult to fix
  # this breaks makepkg, specifically on the linux PKGBUILD's patch-*.xz

  ./configure \
    --prefix=/usr \
    --datadir=/usr/share/file \
    --enable-fsect-man5 \
    --disable-libseccomp
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
