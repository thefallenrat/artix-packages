# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Judd <judd@archlinux.org>

pkgname=nano
pkgver=4.0
pkgrel=2
pkgdesc="Pico editor clone with enhancements"
arch=('x86_64')
license=('GPL')
url="https://www.nano-editor.org"
groups=('base')
depends=('ncurses' 'file' 'sh')
backup=('etc/nanorc')
source=(https://www.nano-editor.org/dist/v4/${pkgname}-${pkgver}.tar.xz{,.asc}
        0001-files-initialize-a-variable-before-referencing-it.patch)
sha256sums=('1e2fcfea35784624a7d86785768b772d58bb3995d1aec9176a27a113b1e9bac3'
            'SKIP'
            'f1dce36d1932d925ae3491ab19d3032263fe7c675d0f9d69f30adece6441da96')
validpgpkeys=('8DA6FE7BFA7A418AB3CB2354BCB356DF91009FA7' # "Chris Allegretta <chrisa@asty.org>"
              'A7F6A64A67DA09EF92782DD79DF4862AF1175C5B' # "Benno Schulenberg <bensberg@justemail.net>"
              'BFD009061E535052AD0DF2150D28D4D2A0ACE884' # "Benno Schulenberg <bensberg@telfort.nl>"
)

prepare() {
  cd ${pkgname}-${pkgver}
  # correct a possible non-working ^S keystroke on some systems
  # http://lists.gnu.org/archive/html/nano-devel/2019-04/msg00000.html
  patch -Np1 -i ../0001-files-initialize-a-variable-before-referencing-it.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --enable-color \
    --enable-nanorc \
    --enable-multibuffer
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -DTm644 ${srcdir}/${pkgname}-${pkgver}/doc/sample.nanorc ${pkgdir}/etc/nanorc
}
