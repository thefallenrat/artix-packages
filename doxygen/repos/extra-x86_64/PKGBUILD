# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Dan McGee <dan@archlinux.org>

pkgbase=doxygen
pkgname=('doxygen' 'doxygen-docs')
pkgver=1.8.16
pkgrel=2
pkgdesc='Documentation system for C++, C, Java, IDL and PHP'
url='http://www.doxygen.nl'
arch=('x86_64')
license=('GPL')
makedepends=('cmake' 'gcc-libs' 'flex' 'qt5-base' 'texlive-core' 'ghostscript'
             'texlive-latexextra' 'graphviz' 'python2' 'git')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/doxygen/doxygen/archive/Release_${pkgver//./_}.tar.gz
        doxygen-1.8.16-compat.patch)
sha256sums=('75b18117f88ca1930ab74c05f6712690a26dd4fdcfc9d7d5324be43160645fb4'
            'bb0d5a9baa9ee364da6ba51f5501d5a882572ae15055dc2db41593f3a19cb0e1')
sha512sums=('546ceaae949cf5cc8162309bc804dad7a00b970c14a2dd66d27111e03f2fbebfec299ddc559f8c915ed00776217d2c439843806d2b6c15b58cf29f490979bd8f'
            'c41d5dd0e69d42119c025582ea9beb2bb09cf37f654e5d02aa2f68fea4ecb3465dc1b47d8e6202da1188b44e61ca3b90e848dfdf8464d33674bdc2645ae21144')

prepare() {
  cd ${pkgbase}-Release_${pkgver//./_}
  mkdir build
  # Install the man pages in the right place
  sed -i 's:DESTINATION man/man1:DESTINATION "${CMAKE_INSTALL_PREFIX}/share/man/man1":g' \
    doc/CMakeLists.txt
  patch -Np1 < ../doxygen-1.8.16-compat.patch
}

build() {
  cd ${pkgbase}-Release_${pkgver//./_}/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DDOC_INSTALL_DIR:PATH=share/doc/doxygen \
    -DPYTHON_EXECUTABLE:FILE=/usr/bin/python2 \
    -Dbuild_doc:BOOL=ON \
    -Dbuild_wizard:BOOL=ON
  make
  make docs
}

check() {
  cd ${pkgbase}-Release_${pkgver//./_}/build
  make tests
}

package_doxygen() {
  pkgdesc='Documentation system for C++, C, Java, IDL and PHP'
  depends=('gcc-libs')
  optdepends=('graphviz: for caller/callee graph generation'
              'qt5-base: for doxywizard')

  cd ${pkgbase}-Release_${pkgver//./_}/build
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/usr/share/doc"
}

package_doxygen-docs() {
  pkgdesc='Developer documentation for doxygen'

  cd ${pkgbase}-Release_${pkgver//./_}/build
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/usr/bin"
  rm -rf "${pkgdir}/usr/share/man"
}

# vim: ts=2 sw=2 et:
