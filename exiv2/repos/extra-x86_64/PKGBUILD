# Maintainer: tobias <tobias@arhlinux.org>

pkgname=exiv2
pkgver=0.27.2
pkgrel=1
pkgdesc="Exif, Iptc and XMP metadata manipulation library and tools"
arch=('x86_64')
url="http://exiv2.org"
license=('GPL2')
depends=('gcc-libs' 'zlib' 'expat')
makedepends=('cmake')
source=($pkgname-$pkgver.tar.gz::"https://github.com/Exiv2/exiv2/archive/v$pkgver.tar.gz")
sha256sums=('3dbcaf01fbc5b98d42f091d1ff0d4b6cd9750dc724de3d9c0d113948570b2934')

build() {
  cd $pkgname-$pkgver 
  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DEXIV2_ENABLE_VIDEO=ON \
    -DEXIV2_BUILD_SAMPLES=OFF \
    -DEXIV2_ENABLE_NLS=ON \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
