# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=libcdio-paranoia
pkgver=10.2+2.0.0
pkgrel=2
pkgdesc="CD paranoia libraries from libcdio"
arch=('x86_64')
license=('GPL' 'LGPL')
url="https://www.gnu.org/software/libcdio/"
depends=('libcdio')
source=(https://ftp.gnu.org/gnu/libcdio/${pkgname}-${pkgver}.tar.bz2{,.sig})
sha256sums=('4565c18caf401083c53733e6d2847b6671ba824cff1c7792b9039693d34713c1'
            'SKIP')
validpgpkeys=('DAA63BC2582034A02B923D521A8DE5008275EC21') # R. Bernstein <rocky@panix.com>

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-cpp-progs --disable-static --disable-example-progs
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
}
