# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgbase=vulkan-docs
pkgname=(vulkan-html-docs)
pkgver=1.1.109
pkgrel=1
epoch=1
_pkgname=Vulkan-Docs
arch=(any)
url="https://www.khronos.org/vulkan/"
license=('custom')
makedepends=(asciidoctor python3 dblatex source-highlight cmake ghostscript git)
groups=(vulkan-devel)
source=("${_pkgname}-${pkgver}::https://github.com/KhronosGroup/${_pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('a27f0ea02ad76108933901974ab6950e32f34b3ab2a41a34f961356f5762b047')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  ./makeAllExts html
}

package_vulkan-html-docs() {
  pkgdesc="Vulkan html documentation"
  cd "${_pkgname}-${pkgver}"

  install -dm755 "${pkgdir}/usr/share/doc/vulkan"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"

  mv -v out/*.html "${pkgdir}/usr/share/doc/vulkan/"
  mv -v out/html/* "${pkgdir}/usr/share/doc/vulkan/"

  install -m644 copyright-ccby.txt ${pkgdir}/usr/share/licenses/${pkgname}/copyright-ccby.txt
  install -m644 copyright-spec.txt ${pkgdir}/usr/share/licenses/${pkgname}/copyright-spec.txt
}
