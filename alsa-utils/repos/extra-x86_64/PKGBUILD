# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=alsa-utils
pkgver=1.1.8
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support"
arch=('x86_64')
url="http://www.alsa-project.org"
#can not use >=$pkgver due to use of letters in alsa-lib update versioning
depends=("alsa-lib>1.0.24" 'pciutils' 'ncurses' 'psmisc' 'libsamplerate' 'fftw')
makedepends=('xmlto' 'docbook-xsl' 'eudev')
license=('GPL')
source=(ftp://ftp.alsa-project.org/pub/utils/$pkgname-${pkgver}.tar.bz2)
sha256sums=('fd9bf528922b3829a91913b89a1858c58a0b24271a7b5f529923aa9ea12fa4cf')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-alsaconf --sbindir=/usr/bin \
    --with-udev-rules-dir=/usr/lib/udev/rules.d \
    --with-systemdsystemunitdir=no
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  # dir where to save ALSA state
  install -d "${pkgdir}/var/lib/alsa"
}
