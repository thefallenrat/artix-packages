# Maintainer: artoo <artoo@artixlinux.org>

pkgbase=elogind
pkgname=('elogind' 'libelogind')
pkgver=241.4
pkgrel=1
pkgdesc="The systemd project's logind, extracted to a standalone package"
arch=('x86_64')
url="https://github.com/elogind/elogind"
license=('GPL' 'LGPL2.1')
makedepends=('intltool' 'libtool' 'gperf' 'gtk-doc' 'polkit' 'dbus'
            'libseccomp' 'meson' 'kexec-tools' 'openrc')
options=('!libtool')
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/elogind/elogind/archive/v${pkgver}.tar.gz")
sha256sums=('fb4d0f931d5fb11af5a4426b0198e85fabc83d604ee24427b26480164cddb7e1')

prepare() {
    cd ${pkgbase}-${pkgver}
}

build() {
    local meson_options=(
        -Drootlibdir=/usr/lib
        -Drootlibexecdir=/usr/lib/elogind
        -Ddbuspolicydir=/usr/share/dbus-1/system.d
        -Ddocdir=/usr/share/doc/elogind
        -Ddefault-hierarchy=hybrid
        -Dcgroup-controller=openrc
#         -Ddefault-kill-user-processes=false
    )

    arch-meson "$pkgbase-${pkgver}" build "${meson_options[@]}"

    ninja -C build
}

check(){
    meson test -C build
}

package_elogind() {
    pkgdesc="The systemd project's logind, extracted to a standalone package"
    provides=("elogind=${pkgver}" "systemd=${pkgver}" "systemd-tools=${pkgver}")
    conflicts=('systemd')
    depends=('pam' 'dbus' 'libseccomp' 'libelogind' 'kexec-tools' 'eudev')
    optdepends=('polkit: polkit support')
    backup=('etc/elogind/logind.conf')

    DESTDIR="$pkgdir" meson install -C build

    ln -sfv libelogind.pc ${pkgdir}/usr/lib/pkgconfig/libsystemd.pc

    install -dm755 ${srcdir}/_libelogind
    mv -v ${pkgdir}/usr/lib/libelogind*.so* ${srcdir}/_libelogind
}

package_libelogind(){
    pkgdesc="elogind client libraries"
    groups=('base-devel')
    provides=('libelogind.so' "libelogind=${pkgver}"
            "libsystemd=${pkgver}" "systemd-libs=${pkgver}"
            'libsystemd.so')
    conflicts=('systemd-libs')
    depends=('libcap' 'libeudev')

    cd ${pkgbase}-${pkgver}

    install -dm755 ${pkgdir}/usr/lib
    mv ${srcdir}/_libelogind/libelogind*.so* ${pkgdir}/usr/lib

    ln -sfv libelogind.so ${pkgdir}/usr/lib/libsystemd.so
    ln -sfv libsystemd.so ${pkgdir}/usr/lib/libsystemd.so.0
}
