# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Tom Killian <tom.archlinux.org>
# Contributor: Judd Vinet <jvinet.zeroflux.org>

pkgname=dhcpcd
pkgver=8.1.1
pkgrel=2
pkgdesc="RFC2131 compliant DHCP client daemon"
url="https://roy.marples.name/projects/dhcpcd/"
arch=('x86_64')
license=('BSD')
depends=('glibc' 'sh' 'udev')
optdepends=('openresolv: resolvconf support')
provides=('dhcp-client')
backup=('etc/dhcpcd.conf')
options=('emptydirs')  # We Need the Empty /var/lib/dhcpcd Directory
source=("https://roy.marples.name/downloads/$pkgname/$pkgname-$pkgver.tar.xz"
         fix-ipv4-corruption.patch::https://roy.marples.name/cgit/dhcpcd.git/patch/?id=73ac1843
         validate-ipv6.patch::https://roy.marples.name/cgit/dhcpcd.git/patch/?id=b2b6541f
         validate-ipv4.patch::https://roy.marples.name/cgit/dhcpcd.git/patch/?id=69e2b6c4)
validpgpkeys=('A785ED2755955D9E93EA59F6597F97EA9AD45549')  # Roy Marples (NetBSD) <roy@NetBSD.org>
sha256sums=('485d308fe10febd36b6f936e4260e4ab34a146e4f00a9f7a5509c4377ad5ea82'
            'b0b09efc99ffa952f859f6b86e04c05da8726f55415cf975d8fc13aa1895a4ce'
            '758ec179a4e20958a37dd7d2bd6184b9526cbd45b443bb5ca8531b30356a10ec'
            '8b4258b1dd82ac9399c5c5fc7519a8b90421a68eb8aa5ca3034e0e7d938ad734')

prepare() {
  cd $pkgname-$pkgver
  # Backport fixes FS#64398 FS#64413
  patch -p1 -i ../fix-ipv4-corruption.patch
  patch -p1 -i ../validate-ipv6.patch
  patch -p1 -i ../validate-ipv4.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # configure variables
  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --sbindir=/usr/bin \
      --libexecdir=/usr/lib/dhcpcd \
      --dbdir=/var/lib/dhcpcd \
      --rundir=/run

  # Build
  make
}


check() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make test
}


package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Install License
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/LICENSE" \
	  "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Set Options in /etc/dhcpcd.conf
  echo noipv4ll >> "${pkgdir}/etc/dhcpcd.conf" # Disable ip4vall
}
