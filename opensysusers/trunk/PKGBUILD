# Maintainer: Chris Cromer <cromer@artixlinux.org>
# Maintainer: artoo <artoo@artixlinux.org>

pkgbase=opensysusers
pkgname=(opensysusers opensysusers-openrc opensysusers-docs)
pkgver=0.5
pkgrel=6
pkgdesc="A standalone utility for handling systemd-style sysusers.d users and groups"
arch=('any')
url="https://gitea.artixlinux.org/artix/opensysusers"
license=('BSD2')
makedepends=('docbook-xsl')
source=("${pkgbase}-${pkgver}.tar.gz::https://gitea.artixlinux.org/artix/opensysusers/archive/${pkgver}.tar.gz"
		'20-opensysusers.hook'
		'sysusers-hook'
		fix-arg-length.patch::https://gitea.artixlinux.org/artix/opensysusers/commit/8ad79b763c19df9e1fc96098e7e7d398df436492.patch
		fix-arg.patch::https://gitea.artixlinux.org/artix/opensysusers/commit/71b089f066304cb31946eb171754fa14c750c1af.patch)
sha256sums=('c9c012063e073d5589a83aa5d144010fa5214aa3b7340837f8138c8181e60dfe'
            'f6ae7c3cfc5e21b8a4c950de5ef314f38b4ec9b3527bd35278afb5d823564849'
            '4222b2496d8bb1ac9c07707e4ef1b334b8b59eb15451ea8ba0fd6e4c93e50c56'
            '20d60b9c3baa827602a8658fc55eb5c723ebcbd6f0c68ad2fe09134dc887d622'
            'bc27ea36428272da849ebb7dfcec2b498666df48175b2896f11cec43a1a0d9b6')

prepare(){
    cd "${pkgbase}"
    patch -Np 1 -i $srcdir/fix-arg-length.patch
    patch -Np 1 -i $srcdir/fix-arg.patch
}

build(){
    cd "${pkgbase}"
    make PREFIX=/usr HAVEOPENRC=yes
}

package_opensysusers() {
    depends=('pacman')
    optdepends=('opensysusers-openrc: Opensysusers init script')

    cd "${pkgbase}"
    make PREFIX=/usr DESTDIR="${pkgdir}" install-shared install-default-bin

    # pacman hooks
    install -Dm755 ${srcdir}/sysusers-hook "$pkgdir"/usr/share/libalpm/scripts/sysusers-hook
    install -Dm644 -t "$pkgdir"/usr/share/libalpm/hooks ${srcdir}/*.hook

    ln -snf "/usr/bin/sysusers" "${pkgdir}/usr/bin/systemd-sysusers"
}

package_opensysusers-openrc(){
    pkgdesc="OpenRC opensysusers init script"
    provides=('init-opensysusers')
    depends=('opensysusers')
    conflicts=('init-opensysusers')

    cd "${pkgbase}"
    make PREFIX=/usr DESTDIR="${pkgdir}" install-openrc

    sed -e 's|#!/sbin/openrc-run|#!/usr/bin/openrc-run|g' \
            -i ${pkgdir}/etc/init.d/opensysusers

    install -dm755 ${pkgdir}/etc/runlevels/boot
    ln -snf "/etc/init.d/opensysusers" "${pkgdir}/etc/runlevels/boot/opensysusers"
}

package_opensysusers-docs(){
    pkgdesc="Opensysusers man"
    depends=('opensysusers')

    cd "${pkgbase}"
    make PREFIX=/usr DESTDIR="${pkgdir}" install-man
}
