# Maintainer: Chris Cromer <cromer@artixlinux.org>
# Maintainer: artoo <artoo@artixlinux.org>

pkgbase=opensysusers
pkgname=(opensysusers opensysusers-openrc opensysusers-docs)
pkgver=0.5.1
pkgrel=1
pkgdesc="A standalone utility for handling systemd-style sysusers.d users and groups"
arch=('any')
url="https://gitea.artixlinux.org/artix/opensysusers"
license=('BSD2')
makedepends=('docbook-xsl')
source=("${pkgbase}-${pkgver}.tar.gz::https://gitea.artixlinux.org/artix/opensysusers/archive/${pkgver}.tar.gz"
		'20-opensysusers.hook'
		'sysusers-hook')
sha256sums=('e941ad0f8cb8b576d434c2880ef9374ba44f28a05a9325a55f09aab0974e3de3'
            '8085d3f8e7b2030cef5e0f50622121ea161a2d9d087664f0a11d36f275b24970'
            '4222b2496d8bb1ac9c07707e4ef1b334b8b59eb15451ea8ba0fd6e4c93e50c56')

prepare(){
    cd "${pkgbase}"
}

build(){
    cd "${pkgbase}"
    make PREFIX=/usr HAVEOPENRC=yes
}

package_opensysusers() {
    depends=('pacman')
    optdepends=('opensysusers-openrc: Opensysusers init script')

    cd "${pkgbase}"
    make PREFIX=/usr DESTDIR="${pkgdir}" install-shared install-default-bin

    # pacman hooks
    install -Dm755 ${srcdir}/sysusers-hook "$pkgdir"/usr/share/libalpm/scripts/sysusers-hook
    install -Dm644 -t "$pkgdir"/usr/share/libalpm/hooks ${srcdir}/*.hook

    ln -snf "/usr/bin/sysusers" "${pkgdir}/usr/bin/systemd-sysusers"
}

package_opensysusers-openrc(){
    pkgdesc="OpenRC opensysusers init script"
    provides=('init-opensysusers')
    depends=('opensysusers')
    conflicts=('init-opensysusers')

    cd "${pkgbase}"
    make PREFIX=/usr DESTDIR="${pkgdir}" install-openrc

    sed -e 's|#!/sbin/openrc-run|#!/usr/bin/openrc-run|g' \
            -i ${pkgdir}/etc/init.d/opensysusers

    install -dm755 ${pkgdir}/etc/runlevels/boot
    ln -snf "/etc/init.d/opensysusers" "${pkgdir}/etc/runlevels/boot/opensysusers"
}

package_opensysusers-docs(){
    pkgdesc="Opensysusers man"
    depends=('opensysusers')

    cd "${pkgbase}"
    make PREFIX=/usr DESTDIR="${pkgdir}" install-man
}
