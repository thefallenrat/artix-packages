# Maintainer: Chris Cromer <cromer@artixlinux.org>
# Maintainer: artoo <artoo@artixlinux.org>

pkgbase=opensysusers
pkgname=(opensysusers opensysusers-docs)
pkgver=0.5.1
pkgrel=4
pkgdesc="A standalone utility for handling systemd-style sysusers.d users and groups"
arch=('any')
url="https://gitea.artixlinux.org/artix/opensysusers"
license=('BSD2')
makedepends=('docbook-xsl' 'git')
_commit=6de96f49e4854b964fc81acdd117199981ee3ad3
source=("git+https://gitea.artixlinux.org/artix/opensysusers.git#commit=${_commit}"
		'20-opensysusers.hook'
		'sysusers-hook')
sha256sums=('SKIP'
            '69733b608efa7a3cbd3fac0daa0e7ba2ffdf50ec535223a04127cb96def6cb57'
            '4222b2496d8bb1ac9c07707e4ef1b334b8b59eb15451ea8ba0fd6e4c93e50c56')

prepare(){
    cd "${pkgbase}"
}

build(){
    cd "${pkgbase}"
    make PREFIX=/usr HAVEOPENRC=no
}

package_opensysusers() {
    depends=('pacman')

    cd "${pkgbase}"
    make PREFIX=/usr DESTDIR="${pkgdir}" install-shared install-default-bin

    # pacman hooks
    install -Dm755 ${srcdir}/sysusers-hook "$pkgdir"/usr/share/libalpm/scripts/sysusers-hook
    install -Dm644 -t "$pkgdir"/usr/share/libalpm/hooks ${srcdir}/*.hook

    ln -snf "/usr/bin/sysusers" "${pkgdir}/usr/bin/systemd-sysusers"
}

package_opensysusers-docs(){
    pkgdesc="Opensysusers man"
    depends=('opensysusers')

    cd "${pkgbase}"
    make PREFIX=/usr DESTDIR="${pkgdir}" install-man
}
