# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=tslib
pkgver=1.21
pkgrel=1
pkgdesc="Touchscreen Access Library"
arch=(x86_64)
url="https://github.com/kergoth/tslib"
license=(GPL2)
depends=(glibc)
makedepends=()
source=("https://github.com/kergoth/tslib/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"{,.asc})
sha256sums=('d2a57b823ea59e53a3b130eef05dfed1190b857854f886eec764e1ca1957cf56'
            'SKIP')
validpgpkeys=(F2082B880F9E423934686E3F500398DF5AB387D3) # Martin Kepplinger <martink@posteo.de>

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
