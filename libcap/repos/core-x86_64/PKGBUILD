# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=libcap
pkgver=2.32
pkgrel=1
pkgdesc='POSIX 1003.1e capabilities'
arch=(x86_64)
url='https://sites.google.com/site/fullycapable/'
license=(GPL2)
depends=(glibc attr)
makedepends=(linux-api-headers)
provides=(libcap.so)
source=(https://kernel.org/pub/linux/libs/security/linux-privs/libcap2/libcap-$pkgver.tar.{xz,sign})
sha256sums=('1005e3d227f2340ad1e3360ef8b69d15e3c72a29c09f4894d7aac038bd26e2be'
            'SKIP')
validpgpkeys=(38A644698C69787344E954CE29EE848AE2CCF3F4) # Andrew G. Morgan <morgan@kernel.org>

prepare() {
  cd $pkgname-$pkgver
  sed -i "/SBINDIR/s#sbin#bin#" Make.Rules

  # use our buildflags
  sed -i "s/CFLAGS :=/CFLAGS += \$(CPPFLAGS) /" Make.Rules
  sed -i "s/LDFLAGS :=/LDFLAGS +=/" Make.Rules
}

build() {
  make -C $pkgname-$pkgver KERNEL_HEADERS=/usr/include
}

package() {
  cd $pkgname-$pkgver
  make prefix=/usr lib=/lib DESTDIR="$pkgdir" RAISE_SETFCAP=no install

  install -Dm644 pam_cap/capability.conf \
    "$pkgdir"/usr/share/doc/$pkgname/capability.conf.example
}
