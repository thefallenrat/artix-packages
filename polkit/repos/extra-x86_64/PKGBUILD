# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=polkit
pkgver=0.116
pkgrel=2
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=(x86_64)
license=(LGPL)
url="https://www.freedesktop.org/wiki/Software/polkit/"
depends=(glib2 pam expat elogind js60)
makedepends=(intltool gtk-doc gobject-introspection git autoconf-archive)
_commit=941e9329f8d3d43ace8335d206365b212824e686  # tags/0.116
source=("git+https://gitlab.freedesktop.org/polkit/polkit.git#commit=$_commit"
        '99-artix.rules'
        'elogind-configure-fix.patch')
sha256sums=('SKIP'
            'd28d6ecaf7682860e2b3768bb266bd94313546b7cd0ef3c2eace7b1576c47adb'
            'd90b2a95aa3e9e1ebf9369c038c144e04900e095c5405bc4ba4901c31bf3f59b')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np 1 -i ${srcdir}/elogind-configure-fix.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib \
      --enable-libsystemd-login=no --disable-static \
      --enable-gtk-doc --with-os-type=redhat \
      --enable-libelogind=yes --with-systemdsystemunitdir=no

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname
  make -k check || :
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  install -d -o root -g 102 -m 750 "$pkgdir"/{etc,usr/share}/polkit-1/rules.d

  install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf" <<END
u polkitd 102 "PolicyKit daemon"
m polkitd proc
END

  install -m0644 "${srcdir}"/99-artix.rules "${pkgdir}"/etc/polkit-1/rules.d
}

# vim: ts=2 sw=2 et:
