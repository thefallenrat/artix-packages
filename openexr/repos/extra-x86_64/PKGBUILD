# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=openexr
pkgver=2.4.1
pkgrel=2
pkgdesc="An high dynamic-range image file format library"
url="https://www.openexr.com/"
arch=('x86_64')
license=('BSD')
depends=('zlib')
makedepends=('cmake' 'fltk' 'python' 'boost' 'freeglut' 'python-numpy' 'chrpath')
optdepends=('fltk: for exrdisplay' 'boost-libs: python support' 'python: python support') 
conflicts=('ilmbase')
replaces=('ilmbase')
source=($pkgname-$pkgver.tar.gz::"https://github.com/openexr/openexr/archive/v$pkgver.tar.gz"
        openexr-pc-prefix.patch::"https://github.com/AcademySoftwareFoundation/openexr/commit/0b26a9de.patch")
md5sums=('f7f7f893cf38786f88c306dec127113f'
         '3004c4731804a0eb98044a32199bd5b3')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../openexr-pc-prefix.patch # Fix prefix in pc file
}

build() {
  mkdir -p build

  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  install -D -m644 "$srcdir"/$pkgname-$pkgver/LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"

# Install missing python module
  _pythonpath=`python -c "from sysconfig import get_path; print(get_path('platlib'))"`
  install -Dm755 python3*/imathnumpy.so -t "$pkgdir"/$_pythonpath 
}
