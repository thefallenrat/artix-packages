# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=libuv
pkgver=1.30.0
pkgrel=1
pkgdesc="Multi-platform support library with a focus on asynchronous I/O"
arch=('x86_64')
url="https://github.com/libuv/libuv"
license=('custom')
depends=('glibc' 'libnsl')
makedepends=('python-sphinx')
source=("https://github.com/libuv/libuv/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('3ba158cea5027fe7c30314da08a620a718cb30ca38997e46ad00912eefac9d2d94f9dd256c204771686a546d7445da2db1be2a0abed7ba14f122332767ee5ee8')

build() {
	cd $pkgname-$pkgver
	./autogen.sh
	./configure --prefix=/usr
	make
	make man -C docs
}

check() {
	cd $pkgname-$pkgver
	make check || :
}

package() {
	cd $pkgname-$pkgver

	make DESTDIR="$pkgdir" install

	install -Dm644 LICENSE \
		"$pkgdir"/usr/share/licenses/$pkgname/LICENSE
	install -Dm644 AUTHORS \
		"$pkgdir"/usr/share/doc/$pkgname/AUTHORS
	install -Dm644 README.md \
		"$pkgdir"/usr/share/doc/$pkgname/README.md
	install -Dm644 ChangeLog \
		"$pkgdir"/usr/share/doc/$pkgname/ChangeLog
	install -Dm644 docs/build/man/libuv.1 \
		"$pkgdir"/usr/share/man/man1/libuv.1
}
