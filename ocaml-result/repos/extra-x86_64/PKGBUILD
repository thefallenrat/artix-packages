# Maintainer: Jürgen Hötzel <juergen@archlinux.org>

_pkgname=result
pkgname=ocaml-${_pkgname}
pkgver=1.4
pkgrel=2
pkgdesc="Compatibility Result module"
arch=('i686' 'x86_64' 'armv7h')
url="https://github.com/janestreet/result"
license=('BSD')
depends=('ocaml')
makedepends=('ocaml-findlib' 'dune')
options=('!strip')
source=("https://github.com/janestreet/result/archive/${pkgver}.tar.gz")
sha256sums=('167029f0d0475106200697f3dffda20b2462a345fe35b449fe86f1f92db354b2')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  dune build
}


package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  install -dm755 "${pkgdir}$(ocamlfind printconf destdir)"
  dune install --prefix "${pkgdir}/usr" \
    --libdir "${pkgdir}$(ocamlfind printconf destdir)"

  install -Dm644 "LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
  mv "${pkgdir}/usr/doc" "${pkgdir}/usr/share/"
}
