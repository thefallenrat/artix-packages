# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libnotify
pkgver=0.7.8
pkgrel=1
pkgdesc="Library for sending desktop notifications"
arch=(x86_64)
url="https://developer.gnome.org/notification-spec/"
license=(LGPL)
depends=(gdk-pixbuf2)
makedepends=(gtk3 gobject-introspection git meson docbook-xsl xmlto gtk-doc)
_commit=44f92222124b12987406d0768e760ecf9f7e27f8  # tags/0.7.8^0
source=("git+https://gitlab.gnome.org/GNOME/libnotify.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
