# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=rubygems
pkgdesc='Package management framework for Ruby'
pkgver=3.0.3
pkgrel=1
arch=(any)
url='https://rubygems.org/'
license=(MIT)
# To avoid circular dependency with ruby, move it to makedepends
makedepends=(ruby)
backup=(etc/gemrc)
checkdepends=(rake)
source=(rubygems-$pkgver.zip::https://github.com/rubygems/rubygems/archive/v${pkgver}.zip
	no-crash-if-missing-rdoc.patch
	gemrc)
sha512sums=('3c73bae27a6bc687b959988787bd4ca033bd0878695e693b9e8919cc10cbb58fcbfdaa83c3528947a60611561cf9f18f536b68b1c76288ec7f6a3045dc3ba42e'
            '0a5f03fc9de0fc4aa4d234d7baf2ffe6dba56850e94ccfba4d73495b0c18aa012ae195e1a0f7aaefb1205a1539a66242d47712dd8d8ae93fc46b9e16af6f91ba'
            '8cafd14d414ee3c16aa94f79072bc6c100262f925dc1300e785846c3fabbbbffc1356b8e2223af5684e3340c55032d41231179ffa948bb12e01dbae0f4131911')

prepare() {
  cd rubygems-${pkgver}

  # workaround for https://github.com/rubygems/rubygems/issues/2483
  patch -p1 < ../no-crash-if-missing-rdoc.patch
}

check() {
  cd rubygems-${pkgver}

  # disable as it requires a bund of ruby dependencies
  #rake test
}

package() {
  depends+=(ruby)
  cd rubygems-${pkgver}

  install -D -m644 ${srcdir}/gemrc "${pkgdir}/etc/gemrc"

  local rubyver=$(ruby --version | sed -r 's|^ruby ([[:digit:]]+\.[[:digit:]]+\.).*$|\1|')0

  install -d "${pkgdir}/usr/lib/ruby/${rubyver}/"
  install -D -m755 bin/gem "${pkgdir}/usr/bin/gem"
  cp -r lib/* "${pkgdir}/usr/lib/ruby/${rubyver}/"
  install -D -m644 MIT.txt "${pkgdir}/usr/share/licenses/rubygems/MIT.txt"
}
