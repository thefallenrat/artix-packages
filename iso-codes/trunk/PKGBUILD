# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=iso-codes
pkgver=4.3
pkgrel=1
pkgdesc="Lists of the country, language, and currency names"
arch=('any')
url="https://salsa.debian.org/iso-codes-team/iso-codes"
license=('LGPL')
makedepends=('python' git)
_commit=43398a317371e309361ce43072603863cb2f57e1  # tags/iso-codes-4.3^0
source=("git+https://salsa.debian.org/iso-codes-team/iso-codes/#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^iso-codes-//;s/-/+/g'
}

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd "$srcdir/$pkgname"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir" pkgconfigdir=/usr/lib/pkgconfig install
}

