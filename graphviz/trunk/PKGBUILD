# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=graphviz
pkgver=2.42.2
_commit=da4c2ec6f24ca1b6d1752c6b5bc4389e55682147
pkgrel=1
pkgdesc='Graph visualization software'
url='https://www.graphviz.org/'
license=('EPL')
arch=('x86_64')
depends=('libltdl' 'gd' 'librsvg' 'libxaw' 'ghostscript' 'pango' 'gts' 'gsfonts' 'libnsl')
makedepends=('swig' 'mono' 'guile' 'lua' 'ocaml' 'perl' 'python2' 'r' 'tk' 'qt5-base' 'gtk2' 'git')
optdepends=('mono: sharp bindings'
            'guile: guile bindings'
            'lua: lua bindings'
            'ocaml: ocaml bindings'
            'perl: perl bindings'
            'python2: python bindings'
            'r: r bindings'
            'tcl: tcl bindings'
            'qt5-base: gvedit'
            'gtk2: gtk output plugin'
            'xterm: vimdot')
source=("git+https://gitlab.com/graphviz/graphviz.git#commit=$_commit"
        'ghostscript918.patch')
sha256sums=('SKIP'
            '0083d126e27f2223ec4226fc1d71c9c84106968a0fdf65de838aee1e4882bfdb')

install=install

prepare() {
	cd "${srcdir}/${pkgname}"
	patch -p1 -i ../ghostscript918.patch
	./autogen.sh
}

build() {
	cd "${srcdir}/${pkgname}"
	export PYTHON=python2
	export LIBPOSTFIX=/
	export CXXFLAGS+=' -fPIC'

	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}"
	make DESTDIR="${pkgdir}" install

	# Deduplicates TCL libraries
	cd "${pkgdir}/usr/lib/tcl8.6"
	rm -fr graphviz
	ln -s ../graphviz/tcl graphviz
}
