# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=graphviz
pkgver=2.40.1
pkgrel=17
pkgdesc='Graph visualization software'
url='https://www.graphviz.org/'
license=('EPL')
arch=('x86_64')
depends=('libltdl' 'gd' 'librsvg' 'libxaw' 'ghostscript' 'pango' 'gts' 'gsfonts' 'libnsl')
makedepends=('swig' 'mono' 'guile' 'lua' 'ocaml' 'perl' 'python2' 'r' 'tk' 'qt5-base' 'gtk2')
optdepends=('mono: sharp bindings'
            'guile: guile bindings'
            'lua: lua bindings'
            'ocaml: ocaml bindings'
            'perl: perl bindings'
            'python2: python bindings'
            'r: r bindings'
            'tcl: tcl bindings'
            'qt5-base: gvedit'
            'gtk2: gtk output plugin'
            'xterm: vimdot')

# http://www.graphviz.org/Download_source.php
source=("https://graphviz.gitlab.io/pub/graphviz/stable/SOURCES/graphviz.tar.gz"
        'graphviz-qt5.patch'
        'ghostscript918.patch')
sha256sums=('ca5218fade0204d59947126c38439f432853543b0818d9d728c589dfe7f3a421'
            '447de21d9c54aaa28a8ae6e35e7de1d3cc2fb45e4c3e5ca9e246e6e219d72735'
            '0083d126e27f2223ec4226fc1d71c9c84106968a0fdf65de838aee1e4882bfdb')

install=install

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../ghostscript918.patch
        patch -p1 -i ../graphviz-qt5.patch # Port to Qt5
        ./autogen.sh
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	export PYTHON=python2
	export LIBPOSTFIX=/
        export CXXFLAGS+=' -fPIC'

	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	# Deduplicates TCL libraries
	cd "${pkgdir}/usr/lib/tcl8.6"
	rm -fr graphviz
	ln -s ../graphviz/tcl graphviz
}
