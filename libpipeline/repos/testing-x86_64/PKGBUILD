# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libpipeline
pkgver=1.5.1
pkgrel=1
pkgdesc="a C library for manipulating pipelines of subprocesses in a flexible and convenient way"
arch=('x86_64')
url="https://libpipeline.nongnu.org/"
license=('GPL')
depends=('glibc')
source=(https://download.savannah.gnu.org/releases/libpipeline/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('d633706b7d845f08b42bc66ddbe845d57e726bf89298e2cee29f09577e2f902f'
            'SKIP')
validpgpkeys=('AC0A4FF12611B6FCCF01C111393587D97D86500B') # Colin Watson <cjwatson@debian.org>

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
}
