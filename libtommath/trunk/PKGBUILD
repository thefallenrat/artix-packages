# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: suasageandeggs <s_stoakley@hotmail.co.uk>
# Contributor: Michael Fellinger <manveru@www.weez-int.com>

pkgname=libtommath
pkgver=1.2.0
pkgrel=1
pkgdesc="Highly optimized and portable routines for integer based number theoretic applications"
url="http://www.libtom.org/"
license=('custom')
arch=('x86_64')
depends=('glibc')
options=(staticlibs)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/libtom/libtommath/archive/v$pkgver.tar.gz")
sha256sums=('f3c20ab5df600d8d89e054d096c116417197827d12732e678525667aa724e30f')

build() {
  cd $pkgname-$pkgver

  make -f makefile.shared IGNORE_SPEED=1
}

package() {
  cd $pkgname-$pkgver

  make -f makefile.shared PREFIX=/usr DESTDIR="$pkgdir" INSTALL_GROUP=root install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
