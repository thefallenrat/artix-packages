# Maintainer: 
# Contributor:	Jesse Jaara	<gmail.com: jesse.jaara>
# Contributor:	Jussi Timperi	<gmail.com: jussi.timperi>

pkgname=libvoikko
pkgver=4.2
pkgrel=2
pkgdesc="A spelling and grammar checker, hyphenator and collection of related linguistic data for Finnish language"
arch=(x86_64)
url="http://voikko.sourceforge.net"
license=(GPL2)
depends=(gcc-libs)
makedepends=(python)
source=(https://www.puimula.org/voikko-sources/libvoikko/$pkgname-$pkgver.tar.gz{,.asc})
sha512sums=('483d3c259a52ae772d52f708789c3df39a4857188c7b9035ce5db8b16bfa4ef144e2ffd557dd6a0655e4c9a6984529784e4098bc949a1bc8a0a50529fee49656'
            'SKIP')
validpgpkeys=('AC5D65F10C8596D7E2DAE2633D309B604AE3942E') # Harri Pitk√§nen <hatapitk@iki.fi>

build() {
  cd $pkgname-$pkgver

# Workaround encoding error
  export LANG=en_US.UTF-8
  ./configure --prefix=/usr --enable-hfst=false --with-dictionary-path=/usr/share/voikko
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir"/usr/lib/python3.8/site-packages
  cp python/libvoikko.py "$pkgdir"/usr/lib/python3.8/site-packages/libvoikko.py
}

