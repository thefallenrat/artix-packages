# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Paul Mattal <paul@mattal.com>

pkgname=help2man
pkgver=1.47.13
pkgrel=1
pkgdesc="Conversion tool to create man files"
arch=('x86_64')
url="https://www.gnu.org/software/help2man/"
license=('GPL')
depends=('perl-locale-gettext')
source=("https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha512sums=('7bf4c75a42c103dd8413a94505f383c6e2b215f9a4cc506e11935acc7f108caad01a5fa489ee157ebef5fa02299145592f65cc828fc016417af557382975d57a'
            'SKIP')
validpgpkeys=('87EA44D150D89615E39A3FEEF0DC8E00B28C5995') # Brendan O'Dea <bod@debian.org>

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
	      --infodir=/usr/share/info \
	      --libdir=/usr/lib
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
