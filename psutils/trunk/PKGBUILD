# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: eric <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
 
pkgname=psutils
pkgver=1.93
pkgrel=1
arch=('x86_64')
pkgdesc="A set of postscript utilities"
url="https://github.com/rrthomas/psutils"
license=('custom')
depends=('glibc' 'ghostscript')
provides=('psutils-lprng')
replaces=('psutils-lprng')
options=('!makeflags')
source=("https://github.com/rrthomas/psutils/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('48da2ec1be379ac05d0d3d5d7b6229c2fa56dfb41ae811f5120021e832db21f4')

build() {
  cd ${pkgname}-${pkgver}
  
  export CFLAGS="$CFLAGS -DPAPER=\\\"letter\\\" -DUNIX -Wall" \
  export CCFLAGS="$LDFLAGS"

  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # install license
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
