# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=libxslt
pkgver=1.1.34
pkgrel=2
pkgdesc="XML stylesheet transformation library"
url="http://xmlsoft.org/XSLT/"
arch=(x86_64)
license=(custom)
depends=(libxml2 libgcrypt)
makedepends=(python2 git)
checkdepends=(docbook-xml python)
_commit=3653123f992db24cec417d12600f4c67388025e3  # tags/v1.1.34^0
source=("git+https://gitlab.gnome.org/GNOME/libxslt.git#commit=$_commit"
        https://sources.debian.org/data/main/libx/libxslt/1.1.34-1/debian/patches/0002-Make-generate-id-deterministic.patch)
sha256sums=('SKIP'
            '371f8a27e57f648315d2ef73499e9bdc15bc3d83956c63b98eb6865f18411b62')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname

  sed -e 's|/usr/bin/python -u|/usr/bin/python2 -u|g' \
      -e 's|/usr/bin/python$|/usr/bin/python2|g' \
      -i python/tests/*.py

  patch -Np1 -i $srcdir/0002-Make-generate-id-deterministic.patch

  NOCONFIGURE=1 ./autogen.sh

}

build() {
  cd $pkgname
  ./configure --prefix=/usr --with-python=/usr/bin/python2
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  find "$pkgdir" -name '*.a' -print -delete
}
