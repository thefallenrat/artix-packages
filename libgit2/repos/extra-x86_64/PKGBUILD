# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: David Runge <dave@sleepmap.de>
# Contributor: Hilton Medeiros <medeiros.hilton@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>

pkgname=libgit2
pkgver=0.28.2
pkgrel=1
epoch=1
pkgdesc='A linkable library for Git'
arch=('x86_64')
url="https://libgit2.github.com/"
depends=('curl' 'http-parser' 'libssh2' 'openssl' 'zlib')
makedepends=('cmake' 'python')
license=('GPL2')
source=("$pkgname-$pkgver.tar.gz::https://github.com/libgit2/libgit2/archive/v${pkgver}.tar.gz"
        "${pkgname}-0.28.2-online-tests.patch::https://github.com/libgit2/libgit2/pull/5094.patch")
sha256sums=('42b5f1e9b9159d66d86fff0394215c5733b6ef8f9b9d054cdd8c73ad47177fc3'
            '0d1d5ceaae10a9dc17894b8f13e957b463e5f5ccde13dc57f19dbc80f5d89de8')

prepare() {
  cd "$pkgname-$pkgver"
  mkdir -p build
  # fix online tests: https://github.com/libgit2/libgit2/pull/5094
  patch -Np1 -i "../${pkgname}-0.28.2-online-tests.patch"
}

build() {
  cd "$pkgname-$pkgver/build"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DTHREADSAFE=ON \
        ..
  make VERBOSE=1
}

check() {
  cd "$pkgname-$pkgver/build"
  make test VERBOSE=1
}

package() {
  cd "$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir" install
}
