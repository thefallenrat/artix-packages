# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libpwquality
pkgver=1.4.1
pkgrel=1
pkgdesc="Library for password quality checking and generating random passwords"
arch=('x86_64')
url="https://github.com/libpwquality/libpwquality"
license=('GPL')
depends=('cracklib' 'pam')
optdepends=('python: Python bindings')
makedepends=('python' git)
backup=('etc/security/pwquality.conf')
_commit=c6ddfc8cbb8037b6d10177513f6c355c914d9c99  # tags/libpwquality-1.4.1
source=("git+https://github.com/libpwquality/libpwquality.git#commit=$_commit")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^libpwquality-//;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
  sed '/mr_IN/d' -i po/LINGUAS
}

build() {
    cd "$pkgname"
    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-python-binary=python3
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package() {
    cd "$pkgname"
    make DESTDIR="$pkgdir" install
    install -m755 -d "$pkgdir"/etc/security/pwquality.conf.d
}
