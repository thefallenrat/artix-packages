# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gst-plugins-good
pkgver=1.14.4
pkgrel=2
pkgdesc="GStreamer Multimedia Framework Good Plugins"
url="https://gstreamer.freedesktop.org/"
arch=(x86_64)
license=(LGPL)
depends=(libpulse jack libsoup gst-plugins-base-libs wavpack aalib taglib libdv libshout libvpx
         gdk-pixbuf2 libcaca libavc1394 libiec61883 libxdamage v4l-utils cairo libgudev speex flac
         libraw1394 lame mpg123 twolame)
makedepends=(python gtk-doc autoconf-archive git gtk3 qt5-declarative qt5-x11extras qt5-wayland)
checkdepends=(gst-plugins-base)
_commit=d88d1b0e43b140587f35c7c9bf390fae051e427d  # tags/1.14.4^0
source=("git+https://anongit.freedesktop.org/git/gstreamer/gst-plugins-good#commit=$_commit"
        0001-vpx-Fix-build-against-libvpx-1.8.patch
        "gst-common::git+https://anongit.freedesktop.org/git/gstreamer/common")
sha256sums=('SKIP'
            '8ffcb46a5f07d04509b1e81b63d121c63cd68e91409be942a27b9a50279335a8'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  patch -Np1 -i ../0001-vpx-Fix-build-against-libvpx-1.8.patch

  git submodule init
  git config --local submodule.common.url "$srcdir/gst-common"
  git submodule update

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --with-package-name="GStreamer Good Plugins (Artix Linux)" \
    --with-package-origin="https://www.artixlinux.org/" \
    --enable-experimental \
    --enable-gtk-doc \
    --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
