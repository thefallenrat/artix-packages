# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=at-spi2-atk
pkgver=2.30.1
pkgrel=1
pkgdesc="A GTK+ module that bridges ATK to D-Bus at-spi"
url="https://wiki.gnome.org/Accessibility"
arch=(x86_64)
license=(GPL2)
depends=(at-spi2-core libx11 libxml2 atk)
makedepends=(git meson)
_commit=036eccfe71da7b6212d032c9aae601b256bd55ae  # gnome-3-30
source=("git+https://gitlab.gnome.org/GNOME/at-spi2-atk.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^AT_SPI2_ATK_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  # Fixup tag
  git tag -f AT_SPI2_ATK_2_30_1 036eccfe71da7b6212d032c9aae601b256bd55ae
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  dbus-run-session meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set ts=2 sw=2 et:
