# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=liblouis
pkgver=3.11.0
pkgrel=3
pkgdesc="Open-source braille translator and back-translator"
arch=(x86_64)
url="http://liblouis.org/"
license=('GPL3')
depends=(glibc)
makedepends=(help2man python)
source=(https://github.com/liblouis/liblouis/releases/download/v$pkgver/liblouis-$pkgver.tar.gz)
sha256sums=('b802aba0bff49636907ca748225e21c56ecf3f3ebc143d582430036d4d9f6259')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static --enable-ucs4
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cd python
  LD_PRELOAD+=":$srcdir/$pkgname-$pkgver/liblouis/.libs/liblouis.so"
  python setup.py install --root="$pkgdir" --prefix="/usr" --optimize=1
}
# vim:set ts=2 sw=2 et:
