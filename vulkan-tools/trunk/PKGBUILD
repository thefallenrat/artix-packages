# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=vulkan-tools
pkgver=1.1.114
pkgrel=1
arch=(x86_64)
url="https://www.khronos.org/vulkan/"
pkgdesc="Vulkan Utilities and Tools"
license=('custom')
depends=(gcc-libs libxcb vulkan-icd-loader)
makedepends=(git cmake python vulkan-headers libx11 wayland)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/KhronosGroup/Vulkan-Tools/archive/v${pkgver}.tar.gz")
#source=("git+https://github.com/KhronosGroup/Vulkan-Tools.git#commit=534747d770a84a18a20949430679aa1b7bf0f398")
sha256sums=('d40c0db2353d4d6f70a184787ecae75ff53a4f4260e85ef986b2d0acac67c993')

#pkgver() {
#  cd "${srcdir}"/Vulkan-Tools*
#
#  echo 1.1.97.0+$(git rev-list --count HEAD)+$(git rev-parse --short HEAD)
#}

build() {
  cd "${srcdir}"/Vulkan-Tools*

  rm -rf build ; mkdir build ; cd build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_DATADIR=/usr/share \
    -DCMAKE_SKIP_RPATH=True \
    -DBUILD_WSI_XCB_SUPPORT=On \
    -DBUILD_WSI_XLIB_SUPPORT=On \
    -DBUILD_WSI_WAYLAND_SUPPORT=On \
    -DBUILD_CUBE=ON \
    -DBUILD_VULKANINFO=ON \
    -DBUILD_ICD=OFF \
    -DCMAKE_BUILD_TYPE=Release \
    ..
  make
}

package() {
  groups=('vulkan-devel')

  cd "${srcdir}"/Vulkan-Tools*/build

  make DESTDIR="${pkgdir}" install

  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ../LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/
}
