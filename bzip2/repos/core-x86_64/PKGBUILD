# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Judd <jvinet@zeroflux.org> 

pkgname=bzip2
pkgver=1.0.7
pkgrel=2
pkgdesc="A high-quality data compression program"
arch=('x86_64')
license=('custom')
url="https://sourceware.org/bzip2/"
groups=('base')
depends=('glibc' 'sh')
source=(https://sourceware.org/pub/bzip2/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('e768a87c5b1a79511499beb41500bcc4caf203726fff46a6f5f9ad27fe08ab2b'
            'SKIP')
validpgpkeys=('EC3CFE88F6CA0788774F5C1D1AA44BE649DE760A') # Mark Wielaard <mark@klomp.org>
        
build() {
  cd $pkgname-$pkgver

  make -f Makefile-libbz2_so CC="gcc $CFLAGS $LDFLAGS"
  make bzip2 bzip2recover CC="gcc $CFLAGS $LDFLAGS"
}

check() {
  cd $pkgname-$pkgver
  make test
}

package() {
  cd $pkgname-$pkgver

  install -dm755 "$pkgdir"/usr/{bin,lib,include,share/man/man1}

  install -m755 bzip2-shared "$pkgdir"/usr/bin/bzip2
  install -m755 bzip2recover bzdiff bzgrep bzmore "$pkgdir"/usr/bin
  ln -sf bzip2 "$pkgdir"/usr/bin/bunzip2
  ln -sf bzip2 "$pkgdir"/usr/bin/bzcat

  cp -a libbz2.so* "$pkgdir"/usr/lib
  ln -s libbz2.so.$pkgver "$pkgdir"/usr/lib/libbz2.so

  install -m644 bzlib.h "$pkgdir"/usr/include/

  install -m644 bzip2.1 "$pkgdir"/usr/share/man/man1/
  ln -sf bzip2.1 "$pkgdir"/usr/share/man/man1/bunzip2.1
  ln -sf bzip2.1 "$pkgdir"/usr/share/man/man1/bzcat.1
  ln -sf bzip2.1 "$pkgdir"/usr/share/man/man1/bzip2recover.1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
}
