# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

pkgname=elfutils
pkgver=0.178
pkgrel=1
pkgdesc="Utilities to handle ELF object files and DWARF debugging information"
arch=('x86_64')
url="https://sourceware.org/elfutils/"
license=('LGPL3' 'GPL' 'GPL3')
depends=('gcc-libs' 'zlib' 'bzip2' 'xz' 'libmicrohttpd' 'sqlite' 'libarchive' 'curl')
checkdepends=('rpmextract')
options=('staticlibs')
provides=('libelf')
conflicts=('libelf')
replaces=('libelf')
source=(https://sourceware.org/elfutils/ftp/${pkgver}/elfutils-${pkgver}.tar.bz2{,.sig})
sha1sums=('5f52d04105a89e50caf69cea40629c323c1eccd9'
          'SKIP')
validpgpkeys=('47CC0331081B8BC6D0FD4DA08370665B57816A6A')  # Mark J. Wielaard <mark@klomp.org>
validpgpkeys+=('EC3CFE88F6CA0788774F5C1D1AA44BE649DE760A') # Mark Wielaard <mjw@gnu.org>

build() {
  cd ${pkgbase}-${pkgver}

  CFLAGS+=" -g"  # required for test-suite success
  ./configure --prefix=/usr --program-prefix="eu-" --enable-deterministic-archives

  make
}

check() {
  cd ${pkgbase}-${pkgver}

  # Some tests fail due to limited capabilities of build chroot
  make check || :
}

package() {
  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
