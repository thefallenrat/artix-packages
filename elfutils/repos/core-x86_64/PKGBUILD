# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

pkgbase=elfutils
pkgname=(elfutils libelf)
pkgver=0.175
pkgrel=1
pkgdesc="Utilities to handle ELF object files and DWARF debugging information"
arch=('x86_64')
url="https://sourceware.org/elfutils/"
license=('LGPL3' 'GPL' 'GPL3')
depends=('gcc-libs' 'zlib' 'bzip2' 'xz')
options=('staticlibs')
source=(https://sourceware.org/elfutils/ftp/${pkgver}/elfutils-${pkgver}.tar.bz2{,.sig})
sha1sums=('361f835640ecffddc6d4543fb044eb53f673026f'
          'SKIP')
validpgpkeys=('47CC0331081B8BC6D0FD4DA08370665B57816A6A')  # Mark J. Wielaard <mark@klomp.org>

build() {
  cd ${pkgbase}-${pkgver}

  # fix make check with i686 
  # https://sourceware.org/bugzilla/show_bug.cgi?id=22083
  export CFLAGS=${CFLAGS/-fno-plt} # fix make check with i686 

  CFLAGS+=" -g"  # required for test-suite success
  ./configure --prefix=/usr --program-prefix="eu-" --enable-deterministic-archives

  make
}

check() {
  cd ${pkgbase}-${pkgver}

  make check
}

package_elfutils() {
  depends=("libelf=$pkgver-$pkgrel")

  cd ${pkgbase}-${pkgver}

  make DESTDIR="${pkgdir}" install

  mkdir "${srcdir}"/libelf
  mv "${pkgdir}"/usr/{lib,include} "${srcdir}/libelf"
}

package_libelf() {
  pkgdesc="Libraries to handle ELF object files and DWARF debugging information"
  mv libelf "${pkgdir}/usr"
}
