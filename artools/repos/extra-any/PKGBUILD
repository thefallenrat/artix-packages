# Maintainer: artoo <artoo@artixlinux.org>

pkgbase=artools
pkgname=('artools-base' 'artools-pkg' 'artools-iso')
pkgver=0.12.7
pkgrel=4
arch=('any')
pkgdesc='Development tools for Artix'
license=('GPL')
groups=('artools')
url='https://gitea.artixlinux.org/artix/artools'
source=("${pkgbase}-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz"
        comparepkg.patch::https://gitea.artixlinux.org/artix/artools/commit/8d6f12f5a96a03fa3a3bd721c8eb894941adbe01.patch
)
sha256sums=('c7b23700b6cc38319fcba68c4441c6156db30941eaa105e68a63426b2ab879fa'
            '24e59e5e3f0d8630693db4e2ff71e1c3f9b6bcb893d41bd53a0464b7b88e9ce7')

prepare() {
    cd ${pkgbase}
    sed -e "s/^VERSION=.*/VERSION=${pkgver}/" -i Makefile
    patch -Np1 -i ../comparepkg.patch
}

build() {
    cd ${pkgbase}
    make PREFIX=/usr
}

package_artools-base() {
    pkgdesc='Development tools for Artix (base tools)'
    depends=('openssh' 'rsync' 'haveged' 'os-prober' 'gnupg' 'pacman')
    optdepends=('artools-pkg: Artix package tools'
                'artools-iso: Artix iso tools')
    backup=('etc/artools/artools.conf')
    conflicts=('arch-install-scripts')
    install=artools.install

    cd ${pkgbase}
    make PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_artools-pkg() {
    pkgdesc='Development tools for Artix (packaging tools)'
    depends=('artools-base' 'namcap' 'git-subrepo' 'jshon')
    conflicts=('devtools')

    cd ${pkgbase}
    make PREFIX=/usr DESTDIR=${pkgdir} install_pkg
}

package_artools-iso() {
    pkgdesc='Development tools for Artix (ISO tools)'
    depends=('dosfstools' 'libisoburn' 'squashfs-tools' 'mkinitcpio'
            'grub' 'artools-base' 'iso-profiles')
    conflicts=('archiso')

    cd ${pkgbase}
    make PREFIX=/usr DESTDIR=${pkgdir} install_iso
}

