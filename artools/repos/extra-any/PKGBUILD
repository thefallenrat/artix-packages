# Maintainer: artoo <artoo@artixlinux.org>

pkgbase=artools
pkgname=('artools-base' 'artools-pkg' 'artools-iso')
pkgver=0.11.3
pkgrel=1
arch=('any')
pkgdesc='Development tools for Artix'
license=('GPL')
groups=('artools')
url='https://gitea.artixlinux.org/artix/artools'
source=("${pkgbase}-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz"
        typo-fix.patch::https://gitea.artixlinux.org/artix/artools/commit/442aee08df0a234e468c87c47f3d3a56f753f5de.patch)
sha256sums=('b3c1bde2743c285b86331b3733738f3e177274a5b5c9faa5c9b73fc43e562c31'
            '5944d8555e3db765523e63dae5e1c2e7d0176ad1dd1fbaf839fd990dece8207d')

prepare() {
    cd ${pkgbase}
    sed -e "s/^VERSION=.*/VERSION=${pkgver}/" -i Makefile
    patch -Np 1 -i $srcdir/typo-fix.patch
}

build() {
    cd ${pkgbase}
    make PREFIX=/usr
}

package_artools-base() {
    pkgdesc='Development tools for Artix (base tools)'
    depends=('openssh' 'rsync' 'haveged' 'os-prober' 'gnupg' 'pacman')
    optdepends=('artools-pkg: Artix package tools'
                'artools-iso: Artix iso tools')
    backup=('etc/artools/artools.conf')
    install=artools.install

    cd ${pkgbase}
    make PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_artools-pkg() {
    pkgdesc='Development tools for Artix (packaging tools)'
    depends=('artools-base' 'namcap' 'git-subrepo')
    conflicts=('devtools')

    cd ${pkgbase}
    make PREFIX=/usr DESTDIR=${pkgdir} install_pkg
}

package_artools-iso() {
    pkgdesc='Development tools for Artix (ISO tools)'
    depends=('dosfstools' 'libisoburn' 'squashfs-tools' 'mkinitcpio'
            'grub' 'artools-base' 'iso-profiles')

    cd ${pkgbase}
    make PREFIX=/usr DESTDIR=${pkgdir} install_iso
}

