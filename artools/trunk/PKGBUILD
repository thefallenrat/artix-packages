# Maintainer: artoo <artoo@artixlinux.org>

pkgbase=artools
pkgname=('artools-base' 'artools-pkg' 'artools-iso')
pkgver=0.13.4
pkgrel=1
arch=('any')
pkgdesc='Development tools for Artix'
license=('GPL')
groups=('artools')
url='https://gitea.artixlinux.org/artix/artools'
source=("${pkgbase}-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz")
sha256sums=('5fe34e179d0de718307aa491e8fe70a3e1d031532f89153537c9426a2c8c2295')

prepare() {
    cd ${pkgbase}
    sed -e "s/^VERSION=.*/VERSION=${pkgver}/" -i Makefile
}

build() {
    cd ${pkgbase}
    make PREFIX=/usr
}

package_artools-base() {
    pkgdesc='Development tools for Artix (base tools)'
    depends=('openssh' 'rsync' 'haveged' 'os-prober' 'gnupg' 'pacman')
    optdepends=('artools-pkg: Artix package tools'
                'artools-iso: Artix iso tools')
    backup=('etc/artools/artools.conf')
    conflicts=('arch-install-scripts')
    install=artools.install

    cd ${pkgbase}
    make PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_artools-pkg() {
    pkgdesc='Development tools for Artix (packaging tools)'
    depends=('artools-base' 'namcap' 'git-subrepo' 'jshon')
    conflicts=('devtools')

    cd ${pkgbase}
    make PREFIX=/usr DESTDIR=${pkgdir} install_pkg
}

package_artools-iso() {
    pkgdesc='Development tools for Artix (ISO tools)'
    depends=('dosfstools' 'libisoburn' 'squashfs-tools' 'mkinitcpio'
            'grub' 'artools-base' 'iso-profiles')
    conflicts=('archiso')

    cd ${pkgbase}
    make PREFIX=/usr DESTDIR=${pkgdir} install_iso
}

