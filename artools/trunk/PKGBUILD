# Maintainer: artoo <artoo@artixlinux.org>

pkgbase=artools
pkgname=('artools-base' 'artools-pkg' 'artools-iso')
pkgver=0.13
pkgrel=2
arch=('any')
pkgdesc='Development tools for Artix'
license=('GPL')
groups=('artools')
url='https://gitea.artixlinux.org/artix/artools'
source=("${pkgbase}-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz"
        repos-typo-fix.patch::https://gitea.artixlinux.org/artix/artools/commit/a4d006adefccd07bae23cd1abe65cb90dddcf57d.patch)
sha256sums=('9aeaeea50bfeed7a955063f4204767464a23ccdad4033b499a74323e261bb673'
            '6adb7146a010d26e288d1a65c480834c8c924e5f8a573fda57fc97af07e3b339')

prepare() {
    cd ${pkgbase}
    sed -e "s/^VERSION=.*/VERSION=${pkgver}/" -i Makefile
    patch -Np1 -i ../repos-typo-fix.patch
}

build() {
    cd ${pkgbase}
    make PREFIX=/usr
}

package_artools-base() {
    pkgdesc='Development tools for Artix (base tools)'
    depends=('openssh' 'rsync' 'haveged' 'os-prober' 'gnupg' 'pacman')
    optdepends=('artools-pkg: Artix package tools'
                'artools-iso: Artix iso tools')
    backup=('etc/artools/artools.conf')
    conflicts=('arch-install-scripts')
    install=artools.install

    cd ${pkgbase}
    make PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_artools-pkg() {
    pkgdesc='Development tools for Artix (packaging tools)'
    depends=('artools-base' 'namcap' 'git-subrepo' 'jshon')
    conflicts=('devtools')

    cd ${pkgbase}
    make PREFIX=/usr DESTDIR=${pkgdir} install_pkg
}

package_artools-iso() {
    pkgdesc='Development tools for Artix (ISO tools)'
    depends=('dosfstools' 'libisoburn' 'squashfs-tools' 'mkinitcpio'
            'grub' 'artools-base' 'iso-profiles')
    conflicts=('archiso')

    cd ${pkgbase}
    make PREFIX=/usr DESTDIR=${pkgdir} install_iso
}

