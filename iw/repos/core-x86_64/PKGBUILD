# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=iw
pkgver=5.4
pkgrel=1
pkgdesc='nl80211 based CLI configuration utility for wireless devices'
arch=('x86_64')
url='https://wireless.kernel.org/en/users/Documentation/iw'
license=('GPL')
depends=('libnl')
makedepends=('linux-api-headers')
validpgpkeys=('C0EBC440F6DA091C884D8532E0F373F37BF9099A') # Johannes Berg
source=("https://www.kernel.org/pub/software/network/$pkgname/$pkgname-$pkgver.tar."{xz,sign})
sha256sums=('a2469f677088d7b1070a7fbb28f3c747041697e8f6ec70783339cb1bc27a395f'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" SBINDIR="/usr/bin" install
}
