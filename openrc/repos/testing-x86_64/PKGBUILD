# Maintainer: artoo <artoo@artixlinux.org>
# Maintainer: Chris Cromer <cromer@artixlinux.org>
# Contributor: williamh <williamh@gentoo.org>

_url="https://github.com/OpenRC/openrc/archive"

pkgbase=openrc
pkgname=('openrc' 'openrc-bash-completions' 'openrc-zsh-completions')
pkgver=0.40.2
pkgrel=1
pkgdesc="Gentoo's universal init system"
arch=('x86_64')
url="http://www.gentoo.org/proj/en/base/openrc/"
license=('BSD2')
makedepends=('psmisc' 'pam' 'netifrc' 'opentmpfiles-openrc'
            'opensysusers-openrc' 'elogind-openrc' 'zsh')
options=('!emptydirs')
source=("${pkgbase}-${pkgver}.tar.gz::${_url}/${pkgver}.tar.gz"
        "${pkgbase}.logrotate"
        'openrc-hook'
        'openrc-install.hook'
        'openrc-remove.hook'
        'sysctl.hook'
        'sysctl.conf')
sha256sums=('c53dccf062f56c056803ccab468062dec2692295ef799585d635525ac8983ef2'
            '0b44210db9770588bd491cd6c0ac9412d99124c6be4c9d3f7d31ec8746072f5c'
            'efc7bcb19bbaf11372d304006eb81e89c26ca5f2524f7d171f46ada97e6d6001'
            '385d16b3133b53c459a3910a739304cd009478e37b5550f4ce698f7deea774f0'
            'a636d63fa753420cfbcaf451f97f68897e98d471a6f516b8edc14f509e42c013'
            '9065d497c2bcde322154e177607ccf25f6a89582eada99fc6008cb379ae00bca'
            '4170bcf3655ff6fb32b35fc592a4294a64d25df98d2f0f09f467689ed1ad54ea')

_args=(
    SYSCONFDIR=/etc
    PREFIX=/usr
    SBINDIR=/usr/bin
    LIBEXECDIR=/usr/lib/openrc
    MKSELINUX=no
    MKPAM=pam
    MKTERMCAP=ncurses
    MKNET=no
    MKSYSVINIT=no
    MKBASHCOMP=yes
    MKZSHCOMP=yes
    MKPKGCONFIG=yes
)

if [ -f /usr/lib/os-release ]; then
    . /usr/lib/os-release
    _args+=(BRANDING="${NAME}")
else
    _args+=(BRANDING='Unknown Linux')
fi

prepare(){
    cd "${srcdir}/${pkgbase}-${pkgver}"
    sed -i 's:0444:0644:' mk/sys.mk
}

build(){
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make "${_args[@]}"
}

package_openrc() {
    depends=('psmisc' 'pam' 'opentmpfiles-openrc' 'opensysusers-openrc' 'netifrc' 'elogind-openrc')
    optdepends=('networkmanager-openrc: NetworkManager')
    install=${pkgname}.install
    backup=('etc/rc.conf'
            'etc/conf.d/consolefont'
            'etc/conf.d/keymaps'
            'etc/conf.d/hostname'
            'etc/conf.d/modules'
            'etc/conf.d/hwclock'
            etc/conf.d/agetty.tty{1,2,3,4,5,6})
    provides=('svc-manager')
    conflicts=('svc-manager')

    cd "${srcdir}/${pkgbase}-${pkgver}"

    make DESTDIR="${pkgdir}" "${_args[@]}" install

    install -Dm644 "${srcdir}/${pkgbase}.logrotate" "${pkgdir}/etc/logrotate.d/${pkgbase}"

    sed -e 's/#unicode="NO"/unicode="YES"/' \
        -e 's/#rc_logger="NO"/rc_logger="YES"/' \
        -e 's/#rc_controller_cgroups="YES"/rc_controller_cgroups="YES"/' \
        -e 's|#rc_shell=/bin/sh|rc_shell=/usr/bin/sulogin|' \
        -i "${pkgdir}/etc/rc.conf"

    install -d ${pkgdir}/usr/lib/openrc/cache

    # getty
    sed -e 's|#baud=""|baud="38400"|' \
        -e 's|#term_type="linux"|term_type="linux"|' \
        -e 's|#agetty_options=""|agetty_options=""|' \
        -i ${pkgdir}/etc/conf.d/agetty

    for num in 1 2 3 4 5 6;do
        cp -v ${pkgdir}/etc/conf.d/agetty ${pkgdir}/etc/conf.d/agetty.tty$num
        ln -sf /etc/init.d/agetty ${pkgdir}/etc/init.d/agetty.tty$num
        ln -sf /etc/init.d/agetty.tty$num ${pkgdir}/etc/runlevels/default/agetty.tty$num
    done

    # sysctl defaults
    install -Dm755 ${srcdir}/sysctl.conf "$pkgdir"/etc/sysctl.d/50-default.conf

    # pacman hooks
    install -Dm755 ${srcdir}/openrc-hook "$pkgdir"/usr/share/libalpm/scripts/openrc-hook
    install -Dm644 -t "$pkgdir"/usr/share/libalpm/hooks ${srcdir}/*.hook

    # license
    install -Dm644 ${srcdir}/${pkgname}-${pkgver}/LICENSE "$pkgdir/usr/share/licenses/${pkgbase}/LICENSE"

    # split bash completions
    install -d ${srcdir}/_bash-completion/usr/share/bash-completion/completions
    mv -v ${pkgdir}/usr/share/bash-completion/completions/* ${srcdir}/_bash-completion/usr/share/bash-completion/completions

    # split zsh completions
    install -d ${srcdir}/_zsh/usr/share/zsh/site-functions
    mv -v ${pkgdir}/usr/share/zsh/site-functions/* ${srcdir}/_zsh/usr/share/zsh/site-functions
}

package_openrc-bash-completions() {
    depends=('openrc' 'bash')
    pkgdesc="OpenRC completions for Bash"

    mv -v ${srcdir}/_bash-completion/* ${pkgdir}
}

package_openrc-zsh-completions() {
    depends=('openrc' 'zsh')
    pkgdesc="OpenRC completions for ZSH"

    mv -v ${srcdir}/_zsh/* ${pkgdir}
}
