# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=jbig2dec
pkgver=0.18
_gsver=gs951
pkgrel=1
pkgdesc='Decoder implementation of the JBIG2 image compression format'
url='https://jbig2dec.com/'
arch=('x86_64')
license=('GPL3')
depends=('libpng')
source=("https://github.com/ArtifexSoftware/ghostpdl-downloads/releases/download/${_gsver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('9d9da97a54562b092b30c67ec959d1c48f737316ebf3e6a9fbc5a2fcdf635b0703027c90a904e45dedb1d9400281cae9dd424ae78aa47979862791ccceb7077d')

prepare() {
	cd ${pkgname}-${pkgver}
	autoreconf -vfi
}

build() {
	cd ${pkgname}-${pkgver}
	./configure --prefix=/usr
	make
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR="${pkgdir}" install
}
