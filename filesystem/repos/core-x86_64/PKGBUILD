# Maintainer: SÃ©bastien Luttringer
# Contributor: Tom Gundersen <teg@jklm.no>

pkgname=filesystem
pkgver=2019.10
pkgrel=2
pkgdesc='Base Artix Linux files'
arch=('x86_64')
license=('GPL')
url='https://www.artixlinux.org'
depends=('iana-etc')
backup=('etc/crypttab' 'etc/fstab' 'etc/group' 'etc/gshadow' 'etc/host.conf'
        'etc/hosts' 'etc/issue' 'etc/ld.so.conf' 'etc/motd' 'etc/nsswitch.conf'
        'etc/passwd' 'etc/profile' 'etc/resolv.conf' 'etc/securetty'
        'etc/shadow' 'etc/shells')
source=('crypttab' 'fstab' 'group' 'gshadow' 'host.conf' 'hosts'
        'issue' 'ld.so.conf' 'locale.sh' 'motd' 'nsswitch.conf' 'os-release'
        'passwd' 'profile' 'resolv.conf' 'securetty' 'shadow' 'shells' 'sysctl'
        'sysusers' 'tmpfiles' 'artixlinux.svg' 'artixlinux.png')
md5sums=('5fa6674df7645d7f5895f2d12b4ef4e9'
         'e33f6dfdd61978fcb3ddf1431286e05a'
         '7fed1e1fb855e41a6d64d41f8521d69a'
         '1c1e3b08acfa286f4b417c49de3e4366'
         '7d119a9cce152aa182fb3392ddeecea7'
         'a1315ea3e2b64d197b6efaf9c14ff778'
         '8d7f77c9f6f7a82cc89d3feadd45bb7e'
         '5deb9f890a4d08a245e9752ede77271e'
         '580cf8b59eebfaac4fad373ffadc9912'
         'd41d8cd98f00b204e9800998ecf8427e'
         'fdfa843fc6354816f50ea2f17e6dce50'
         '180f59810b106ff33b533f261bd5be4d'
         '5182ac38a0de85da8ade93ef71975ca4'
         '598e4abf063f02d75950e07d1185bc9c'
         '0ee015fad07732676d9488ae498eed41'
         'f04bcb2803afc4dcb95670fe87343b4d'
         'f64466dd77c7bec37a8b47681468211a'
         'a78cd8d7f8240a8448edee82f503c34e'
         'a51847c012555c843dbdf8df0da171d3'
         '08e22c5d70ddcc1345f9ce3f15514ea2'
         '0267a3a463f35eec8a31f40a720dfd86'
         '1b7e0cfbb630433fdff16fffda9cfa5c'
         '7ef98723f854c531c431074ef7b127d3')

package() {
  cd "$pkgdir"

  # setup root filesystem
  for d in boot dev etc home mnt usr var opt srv/http run; do
    install -d -m755 $d
  done
  install -d -m555 proc
  install -d -m555 sys
  install -d -m0750 root
  install -d -m1777 tmp
  # vsftpd won't run with write perms on /srv/ftp
  # ftp (uid 14/gid 11)
  install -d -m555 -g 11 srv/ftp

  # setup /etc and /usr/share/factory/etc
  install -d etc/{ld.so.conf.d,skel,profile.d} usr/share/factory/etc
  for f in fstab group host.conf hosts issue ld.so.conf motd nsswitch.conf \
  passwd resolv.conf securetty shells profile; do
    install -m644 "$srcdir"/$f etc/
    install -m644 "$srcdir"/$f usr/share/factory/etc/
  done
  ln -s ../proc/self/mounts etc/mtab
  for f in gshadow shadow crypttab; do
    install -m600 "$srcdir"/$f etc/
    install -m600 "$srcdir"/$f usr/share/factory/etc/
  done
  touch etc/artix-release
  install -m644 "$srcdir"/locale.sh etc/profile.d/locale.sh
  install -Dm644 "$srcdir"/os-release usr/lib/os-release

  # setup /var
  for d in cache local opt log/old lib/misc empty; do
    install -d -m755 var/$d
  done
  install -d -m1777 var/{tmp,spool/mail}

  # allow setgid games (gid 50) to write scores
  install -d -m775 -g 50 var/games
  ln -s spool/mail var/mail
  ln -s ../run var/run
  ln -s ../run/lock var/lock

  # setup /usr hierarchy
  for d in bin include lib share/{misc,pixmaps} src; do
    install -d -m755 usr/$d
  done
  for d in {1..8}; do
    install -d -m755 usr/share/man/man$d
  done

  # add lib symlinks
  ln -s usr/lib lib
  [[ $CARCH = 'x86_64' ]] && {
    ln -s usr/lib lib64
    ln -s lib usr/lib64
  }

  # add bin symlinks
  ln -s usr/bin bin
  ln -s usr/bin sbin
  ln -s bin usr/sbin

  # setup /usr/local hierarchy
  for d in bin etc games include lib man sbin share src; do
    install -d -m755 usr/local/$d
  done
  ln -s ../man usr/local/share/man

  # setup sysctl
  install -D -m644 "$srcdir"/sysctl usr/lib/sysctl.d/10-artix.conf

  # setup sysusers
  install -D -m644 "$srcdir"/sysusers usr/lib/sysusers.d/artix.conf

  # setup tmpfiles
  install -D -m644 "$srcdir"/tmpfiles usr/lib/tmpfiles.d/artix.conf

  # add logo
  install -D -m644 "$srcdir"/artixlinux.{svg,png} usr/share/pixmaps
}

# vim:set ts=2 sw=2 et:
